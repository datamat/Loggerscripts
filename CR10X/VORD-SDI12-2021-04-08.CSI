;{CR10X-TD}
;-------------------------------------------------------------
; Stationsnr:             35
; Stationsort:            Vordemwald Bestand.
; Logger:                 Dendro-Logger
; Datenkommunikation:     SDI-12
; Zeitfenster:            s. Loggerprogramm VOB.CR1
; Autor:                  markasub ag, Waldner
; Vor-Version:            NUR DENDRO
; Vor-version in Betrieb: 27.4.2007 bis 2.4.2021
; Version:                nur Dendro SDI-12
; in Betrieb:             2.4. bis 8.4.2021 (10 Min-Tabelle)
;                         ab 8.4.2021 (60 Min-Tabelle)
;
; Projekt Nr.:            2
; - 20 Dendrometer Typ 1 am Dendro-Multiplexer messen
; - 12 Dendrometer Typ 2 am Dendro-Multiplexer messen
;
; Flag Usage:
;  Flag 1: Flag high, wenn Port-Grundeinstellung durchgefuehrt.
;  Flag 2:
;  Flag 3:
;  Flag 4:
;  Flag 5:
;  Flag 6:
;  Flag 7:
;  Flag 8: Saugspannungen messen high=ein, low=aus (low)
;
; Ports Usage (Grundeinstellung)
;  Port 1: input
;  Port 2: input
;  Port 3: input
;  Port 4: RES Dendro-Multiplexer (low)
;  Port 5: CLK Dendro-Multiplexer (low)
;  Port 6: high: Typ 1, low: Typ 2 (high)
;  Port 7: input
;  Port 8: SDI-12 Connection

; ---------------------------------------------------------------
; CR10X-Logger VORD mit PakBus OS (Version 10), Adresse 2
; SDI-12 Abfrage mit C! von CR1000X-Logger VOW (PakBus Adresse 50)
;  Elektro-Schema:
;  CR10X.C8 - C1.CR1000X
;  CR10X.G  -  G.CR1000X

;=============================================================
*Table 1 Program
  01: 600       Execution Interval (seconds)

;-------------------------
; Stationsnummer

1:  Z=F (P30)
 1: 35       F
 2: 00       Exponent of 10
 3: 35       Z Loc [ IDStation      ]

; Projektnummer

2:  Z=F (P30)
 1: 2        F
 2: 00       Exponent of 10
 3: 36       Z Loc [ IDProj         ]

; 10-Min Intervall

3:  Z=F (P30)
 1: 60       F
 2: 00       Exponent of 10
 3: 37       Z Loc [ Intervall10    ]



; Call Subroutine 80: Ports-Grundeinstellung

4:  If Flag/Port (P91)
 1: 21       Do if Flag 1 is Low
 2: 80       Call Subroutine 80


;--------------------------
; Messen Dendrometer

; Dendro-Multiplexer einschalten (RES=high)

5:  Do (P86)
 1: 44       Set Port 4 High


; -------------------------------
; 20 Dendrometer Typ 1 messen

; Loop Dendrometer 1-20
; ----
6:  Beginning of Loop (P87)
 1: 0        Delay
 2: 10       Loop Count

; Dendro-Multiplexer weiterschalten
7:  Do (P86)
 1: 75       Pulse Port 5

8:  Step Loop Index (P90)
 1: 2        Step

; Dendrometer messen: Spannung an SE-Channel 1
9:  Excite-Delay (SE) (P4)
 1: 1        Reps
 2: 15       2500 mV Fast Range
 3: 1        SE Channel
 4: 1        Excite all reps w/Exchan 1
 5: 15       Delay (units 0.01 sec)
 6: 2500     mV Excitation
 7: 1     -- Loc [ Dendro_1       ]
 8: 1        Mult
 9: 0        Offset

; Dendrometer messen: Spannung an SE-Channel 3
10:  Excite-Delay (SE) (P4)
 1: 1        Reps
 2: 15       2500 mV Fast Range
 3: 3        SE Channel
 4: 1        Excite all reps w/Exchan 1
 5: 15       Delay (units 0.01 sec)
 6: 2500     mV Excitation
 7: 2     -- Loc [ Dendro_2       ]
 8: 1        Mult
 9: 0        Offset

11:  End (P95)
; --- End Loop Dendrometer 1-20


; -------------------------------
; 12 Dendrometer Typ 2 messen

; Vom Typ 1 auf Typ 2 umschalten (...)
12:  Do (P86)
 1: 56       Set Port 6 Low

; Warten
13:  Excitation with Delay (P22)
 1: 1        Ex Channel
 2: 0        Delay W/Ex (units = 0.01 sec)
 3: 100      Delay After Ex (units = 0.01 sec)
 4: 0        mV Excitation

; Loop Dendrometer 21-32
; ----
14:  Beginning of Loop (P87)
 1: 0        Delay
 2: 6        Loop Count

15:  Do (P86)
 1: 75       Pulse Port 5

16:  Step Loop Index (P90)
 1: 2        Step

; Denrometer messen: DIFF-Channel 1
17:  Volt (Diff) (P2)
 1: 1        Reps
 2: 34       250 mV 50 Hz Rejection Range
 3: 1        DIFF Channel
 4: 21    -- Loc [ Dendro_21      ]
 5: 1        Mult
 6: 0        Offset

; Dendrometer messen: DIFF-Channel 2
18:  Volt (Diff) (P2)
 1: 1        Reps
 2: 34       250 mV 50 Hz Rejection Range
 3: 2        DIFF Channel
 4: 22    -- Loc [ Dendro_22      ]
 5: 1        Mult
 6: 0        Offset

19:  End (P95)
; ---- End Loop Dendrometer 21-32

; Zurueckschalten von Typ 2 auf Typ 1
20:  Do (P86)
 1: 46       Set Port 6 High


; Dendro-Multiplexer ausschalten (RES=low)

21:  Do (P86)
 1: 54       Set Port 4 Low

; Ende Messen Dendrometer
;---------------------


;---------------------
; Messen Logger

; Loggertemperatur messen
22:  Internal Temperature (P17)
 1: 33       Loc [ LoggTemp       ]

; Batteriespannung messen
23:  Batt Voltage (P10)
 1: 34       Loc [ BattVolt       ]

; Ende Messen Logger
;---------------------


;---------------------
; Speichern-PB alle 60 Minuten

; Alle 60 Min
24:  If time is (P92)
 1: 0.0      Seconds into a
 2: 3600     Second interval
 3: 30       Then Do


25:  Data Table (P84)^9037
 1: 0.0      Seconds into Interval
 2: 0.0      Interval Option
 3: 0.0      (0 = auto allocate, -x = redirect to inloc x)
 4: P02Dendro60               Table Name

26:  Sample (P70)^5566
 1: 1        Reps
 2: 35       Loc [ IDStation      ]
27:  Sample (P70)^22214
 1: 1        Reps
 2: 36       Loc [ IDProj         ]

28:  Sample (P70)^30188
 1: 1        Reps
 2: 39       Loc [ Intervall60    ]

29:  Sample (P70)^2208
 1: 32       Reps
 2: 1        Loc [ Dendro_1       ]

30:  Sample (P70)^13640
 1: 1        Reps
 2: 33       Loc [ LoggTemp       ]

31:  Sample (P70)^4845
 1: 1        Reps
 2: 34       Loc [ BattVolt       ]


32:  End (P95)


;=============================================================
*Table 2 Program
  02: 0.0000    Execution Interval (seconds)

;=============================================================
*Table 3 Subroutines


; --------------------------------
; Subroutine 80: Ports-Grundeinstellung

1:  Beginning of Subroutine (P85)
 1: 80       Subroutine 80

2:  Do (P86)
 1: 11       Set Flag 1 High

3:  Set Port(s) (P20)
 1: 8810     C8..C5 = input/input/high/low
 2: 888      C4..C1 = low/input/input/input

4:  End (P95)
; -------------------End Subroutine 80


;----------------------------------
; Logger als SDI-12 Sensor laufen lassen
5:  Beginning of Subroutine (P85)
 1: 98       Subroutine 98

; SDI-12 Adresse 0, 34 Werte, ab Inloc 1 bis 34
6:  SDI-12 Sensor (P106)
 1: 0        SDI-12 Address
 2: 34       Time/Values
 3: 1        Loc [ Dendro_1       ]

7:  End (P95)
;-----------------------------------

End Program






-Input Locations-
1 Dendro_1       7 2 1
2 Dendro_2       11 1 1
3 Dendro_3       11 1 0
4 Dendro_4       11 1 0
5 Dendro_5       11 1 0
6 Dendro_6       11 1 0
7 Dendro_7       11 1 0
8 Dendro_8       11 1 0
9 Dendro_9       11 1 0
10 Dendro_10      11 1 0
11 Dendro_11      11 1 0
12 Dendro_12      11 1 0
13 Dendro_13      11 1 0
14 Dendro_14      11 1 0
15 Dendro_15      11 1 0
16 Dendro_16      11 1 0
17 Dendro_17      11 1 0
18 Dendro_18      11 1 0
19 Dendro_19      11 1 0
20 Dendro_20      11 1 0
21 Dendro_21      11 1 1
22 Dendro_22      11 1 1
23 Dendro_23      11 1 0
24 Dendro_24      11 1 0
25 Dendro_25      11 1 0
26 Dendro_26      11 1 0
27 Dendro_27      11 1 0
28 Dendro_28      11 1 0
29 Dendro_29      11 1 0
30 Dendro_30      11 1 0
31 Dendro_31      11 1 0
32 Dendro_32      19 1 0
33 LoggTemp       1 0 1
34 BattVolt       1 1 1
35 IDStation      1 1 1
36 IDProj         1 1 1
37 Intervall10    1 0 1
38 PTemp          1 0 0
39 Intervall60    1 1 0
40 ______________ 0 0 0
41 ______________ 0 0 0
42 ______________ 0 0 0
43 ______________ 0 0 0
44 ______________ 0 0 0
45 ______________ 0 0 0
46 ______________ 0 0 0
47 ______________ 0 0 0
48 ______________ 0 0 0
49 ______________ 0 0 0
50 ______________ 0 0 0
51 ______________ 0 0 0
52 ______________ 0 0 0
53 ______________ 0 0 0
54 ______________ 0 0 0
55 ______________ 0 0 0
56 ______________ 0 0 0
57 ______________ 0 0 0
58 ______________ 0 0 0
59 ______________ 0 0 0
60 ______________ 0 0 0
-Program Security-
0000
0000
0000
-Mode 4-
-CR10X ID-
0
-CR10X Power Up-
3
-CR10X RS-232 Setting-
-1
-PakBus-
1
1
0
0
0
1
0
0
0
0
1
1
0
DR=0
0
0
0
0
.endAddrSwath
.endAddrSwath
.endAddrSwath
.endAddrSwath
-DLD File Labels-
-1
-Final Storage Labels-
0,TMSTAMP,9037
0,RECNBR
1,IDStation~35,5566
2,IDProj~36,22214
3,Intervall60~39,30188
4,Dendro_1~1,2208
4,Dendro_2~2
4,Dendro_3~3
4,Dendro_4~4
4,Dendro_5~5
4,Dendro_6~6
4,Dendro_7~7
4,Dendro_8~8
4,Dendro_9~9
4,Dendro_10~10
4,Dendro_11~11
4,Dendro_12~12
4,Dendro_13~13
4,Dendro_14~14
4,Dendro_15~15
4,Dendro_16~16
4,Dendro_17~17
4,Dendro_18~18
4,Dendro_19~19
4,Dendro_20~20
4,Dendro_21~21
4,Dendro_22~22
4,Dendro_23~23
4,Dendro_24~24
4,Dendro_25~25
4,Dendro_26~26
4,Dendro_27~27
4,Dendro_28~28
4,Dendro_29~29
4,Dendro_30~30
4,Dendro_31~31
4,Dendro_32~32
5,LoggTemp~33,13640
6,BattVolt~34,4845
