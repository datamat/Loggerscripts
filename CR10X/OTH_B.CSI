;{CR10X}
;------------------------------------------------------------------------------
; Stationsname:        WSL31 - LWF 28 - SV02012                      10.12.2009
; Stationsstandort:    Othmarsingen Bestand
; Datenkommunikation:  M20T 079 770 39 93 (10:00 - 10:15, 04:30 - 04:45)
; Table 1 - Projekt 1: MP103A, SK1110, SKP215, SKU430, A100R, W200P, ARG100
; Table 1 - Projekt 2: {DMS/Dial-Dendrometer (AM416, Set 7 - 11)}
; Table 1 - Projekt 3: {Streu CS505, CS205 (AM416, Set 5 - 6)}
; Table 1 - Projekt 4: Service
; Table 1 - Projekt 5: {Boden Clip S3, 107 (AM416, Set 1 - 4)}
; Table 1 - Projekt 6: frei
; Table 1 - Projekt 7: frei
; Table 2 - Projekt 1: frei
; Table 2 - Projekt 2: frei
; Stationsprogramm 7:  Batterie-Ladesteuerung
; Stationsprogramm 8:  Datenkommunikation via M20T
; Stationsprogramm 9: SMS-Warnung Batterieunterspannung
; Autor:               markasub ag (sw/cf/mk)
; Version:             20000828-1
;
; Flag / Port Usage:
; Flag 1: Erzwungene Entladung      Port 1: AM416 Reset
; Flag 2: GSM ein/aus               Port 2: AM416 Clock
; Flag 3:                           Port 3: Dendrometer/Projekt 2 - Speisung ein/aus
;                                           GSM-Switch
; Flag 4:                           Post 4: Shunt ein/aus
; Flag 5:                           Port 5: GSM ein/aus
; Flag 6: GSM-Alarm enable off/on   Port 6: T/rF ein/aus
; Flag 7: Switch controle           Port 7: UV B ein/aus
; Flag 8: X-Min Switch Controle     Port 8: Streufeuchte (ein/aus)
;
; ----------------------------
; Erstellt                                                            13.08.2002
;
; Anpassungen 12.07.2002
; ---
; 1. Windspitze:   ueber 1 s
;                  Hauptprogramm in Table 2 (10 s), da total Execution 5,7 s
;                  Kennungen und Windmessung in Table 1
;                  Speicherung alle 10 min
;
; 2. Lithium Batterie:  Speicherung alle 240 Min
;
; 3. Alle Sensoren der fakultativen Projekte bei "Projekt aktiv", jedoch "Sensor aus" (-999).
;    Sensoren werden gemessen, jedoch wird der Messwert bei "deaktivierten" Sensoren mit -999
;    ueberschrieben.
;
; 4. GSM-Schalter Port 3 / Flag 7/8 - Maximal 30 Minuten
;
; 5. Service-Fenster 13.30 - 14.30
;
; 6. SMS-Alarm:  retry 2 / 60s - Fenster 21:29 bis 21:40 / 11.7 - 12.00 V
;                Alarmfenster wird nur aufgemacht, wenn Batterie 12-11.7 V
; Anpassungen 19.08.2008 (P. Waldner, G. Schneiter):
; ---
; -  Projekt  5: Umhaengen Clip 2 von Set1.H2/L2 auf Set5.H1/L1
;     Anschlussschema:
;        CR10X.S9  - Blitzschutz - AM416.COM.H1 (wie bisher)
;        CR10X.S10 - Blitzschutz - AM416.COM.L1 (wie bisher)
;        CR10X.S11 - Blitzschutz - AM416.COM.H2 (wie bisher)
;        CR10X.E1  - Blitzschutz - AM416.COM.L2 (bisher: CR10X.S12 - AM416.COM.L2)

;        AM416.Set1.H1 - Clip1.Temp                     (nicht installiert)
;        AM416.Set1.L1 - Clip1.rF                       (nicht installiert)
;        AM416.Set2.H1 - Clip2.Temp                     (nicht installiert)
;        AM416.Set2.L1 - Clip2.rF                       (nicht installiert)
;        AM416.Set3.H1 - Bodentemperarur-Stab1.BT-5cm   (nicht installiert)
;        AM416.Set3.L1 - Bodentemperarur-Stab1.BT-10cm  (nicht installiert)
;        AM416.Set3.H2 - Bodentemperarur-Stab1.BT-20cm  (nicht installiert)
;        AM416.Set4.H1 - Bodentemperarur-Stab2.BT-5cm   (nicht installiert)
;        AM416.Set4.L1 - Bodentemperarur-Stab2.BT-10cm  (nicht installiert)
;        AM416.Set4.H2 - Bodentemperarur-Stab2.BT-20cm  (nicht installiert)
;
; -  Projekt 22: ECHO EC-5 Bodenwassergehaltssensoren
;     Anschlussschema Echo_{Standort}_{Tiefe cm}
;        CR10X.C1    - AM416.RES
;        CR10X.C2    - AM416.CLK
;        CR10X.E1    - AM416.L2
;        CR10X.GND   - Echos.Blank
;        AM416.Set12.H1 - gelb  - Echo_1_15.Rot
;        AM416.Set12.L1 - gruen - Echo_1_50.Rot
;        AM416.Set12.H2 - braun - Echo_1_80.Rot
;        AM416.Set12.L2 - weiss - Echo_1_xx.Weiss
;        CR10X.GND      - grau  - Echo_1_xx.Blank
;        AM416.Set13.H1 - gelb  - Echo_2_15.Rot
;        AM416.Set13.L1 - gruen - Echo_2_50.Rot
;        AM416.Set13.H2 - braun - Echo_2_80.Rot
;        AM416.Set13.L2 - weiss - Echo_2_xx.Weiss
;        CR10X.GND     - SCHIRM - Echo_2_xx.Blank
;        AM416.Set14.H1 - gelb  - Echo_3_15.Rot
;        AM416.Set14.L1 - gruen - Echo_3_50.Rot
;        AM416.Set14.H2 - braun - Echo_3_80.Rot
;        AM416.Set14.L2 - weiss - Echo_3_xx.Weiss
;        CR10X.GND      - grau  - Echo_3_xx.Blank
;
; Anpassungen 16.5.2013 (Roman Lim)
; ----
; - 10 Min.-Werte fuer relative Luftfeuchtigkeit und Niederschlag (Projekt 1).
;
;==============================================================================
*Table 1 Program
  01: 1         Execution Interval (seconds)
;------------------------------------------------------------------------------
; Stations- und Projektkennungen

1:  Z=F (P30)
 1: 28       F @@1
 2: 00       Exponent of 10
 3: 1        Z Loc [ Stat_ID   ]

2:  Z=F (P30)
 1: 1        F @@2
 2: 00       Exponent of 10
 3: 2        Z Loc [ Proj1_ID  ]

3:  Z=F (P30)
 1: 0        F @@3
 2: 00       Exponent of 10
 3: 3        Z Loc [ Proj2_ID  ]

4:  Z=F (P30)
 1: 0        F @@4
 2: 00       Exponent of 10
 3: 4        Z Loc [ Proj3_ID  ]

5:  Z=F (P30)
 1: 4        F @@5
 2: 00       Exponent of 10
 3: 5        Z Loc [ Proj4_ID  ]

6:  Z=F (P30)
 1: 0        F @@6
 2: 00       Exponent of 10
 3: 6        Z Loc [ Proj5_ID  ]

7:  Z=F (P30)
 1: 0        F @@7
 2: 00       Exponent of 10
 3: 7        Z Loc [ Proj6_ID  ]

8:  Z=F (P30)
 1: 0        F @@8
 2: 00       Exponent of 10
 3: 8        Z Loc [ Proj7_ID  ]

9:  Z=F x 10^n (P30)
 1: 22       F @@22
 2: 0        n, Exponent of 10
 3: 9        Z Loc [ Proj22_ID ]

;------------------------------------------------------------------------------
; Projekt 1: Sensorkennungen (0 = nicht vorhanden, 1 = vorhanden)
; MP103A, SK1110, SKP215, SKU430, A100R, W200P, ARG100

10:  If (X<=>F) (P89)
 1: 2        X Loc [ Proj1_ID  ]
 2: 1        =
 3: 1        F
 4: 30       Then Do

  11:  Z=F (P30)
   1: 1        F @@10
   2: 00       Exponent of 10
   3: 10       Z Loc [ K_TrF     ]

  12:  Z=F (P30)
   1: 0        F @@11
   2: 00       Exponent of 10
   3: 11       Z Loc [ K_Pyr     ]

  13:  Z=F (P30)
   1: 1        F @@12
   2: 00       Exponent of 10
   3: 12       Z Loc [ K_PAR     ]

  14:  Z=F (P30)
   1: 0        F @@13
   2: 00       Exponent of 10
   3: 13       Z Loc [ K_UV      ]

  15:  Z=F (P30)
   1: 1        F @@14
   2: 00       Exponent of 10
   3: 14       Z Loc [ K_WG      ]

  16:  Z=F (P30)
   1: 0        F @@15
   2: 00       Exponent of 10
   3: 15       Z Loc [ K_WR      ]

  17:  Z=F (P30)
   1: 1        F @@16
   2: 00       Exponent of 10
   3: 16       Z Loc [ K_Ns      ]
;------------------------------------------------------------------------------
;Projekt 1a: Windsensormessungen
; Windgeschwindigkeit [m/s]
  18:  If (X<=>F) (P89)
   1: 14       X Loc [ K_WG      ]
   2: 1        =
   3: 1        F
   4: 30       Then Do

    19:  Pulse (P3)
     1: 1        Reps
     2: 1        Pulse Channel 1
     3: 22       Switch Closure, Output Hz
     4: 25       Loc [ WS_ms     ]
     5: 1.25     Mult
     6: 0.0      Offset

  20:  Else (P94)

    21:  Z=F (P30)
     1: -999     F
     2: 00       Exponent of 10
     3: 25       Z Loc [ WS_ms     ]

  22:  End (P95)

; Windrichtung [Grad]
  23:  If (X<=>F) (P89)
   1: 15       X Loc [ K_WR      ]
   2: 1        =
   3: 1        F
   4: 30       Then Do

    24:  Excite-Delay (SE) (P4)
     1: 1        Reps
     2: 5        2500 mV Slow Range
     3: 4        SE Channel
     4: 2        Excite all reps w/Exchan 2
     5: 1        Delay (units 0.01 sec)
     6: 1000     mV Excitation
     7: 26       Loc [ WR_Grad   ]
     8: 0.36     Mult
     9: 0.0      Offset

  25:  Else (P94)

    26:  Z=F (P30)
     1: -999     F
     2: 00       Exponent of 10
     3: 26       Z Loc [ WR_Grad   ]

  27:  End (P95)

28:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 10       Interval (same units as above)
 3: 10       Set Output Flag High (Flag 0)

29:  Set Active Storage Area (P80)
 1: 3        Input Storage Area
 2: 36       Loc [ WS_mean   ]

30:  Wind Vector (P69)^15582
 1: 1        Reps
 2: 0        Samples per Sub-Interval
 3: 0        S, é1, & å(é1) Polar
 4: 25       Wind Speed/East Loc [ WS_ms     ]
 5: 26       Wind Direction/North Loc [ WR_Grad   ]

31:  Set Active Storage Area (P80)
 1: 3        Input Storage Area
 2: 152      Loc [ Wi_Spitze ]

32:  Maximum (P73)^21844
 1: 1        Reps
 2: 0        Value Only
 3: 25       Loc [ WS_ms     ]

33:  Wind Vector (P69)^21398
 1: 1        Reps
 2: 0        Samples per Sub-Interval
 3: 01       S, é1 Polar
 4: 25       Wind Speed/East Loc [ WS_ms     ]
 5: 26       Wind Direction/North Loc [ WR_Grad   ]

34:  End (P95)

35:  If (X<=>F) (P89)
 1: 15       X Loc [ K_WR      ]
 2: 1        =
 3: 0        F
 4: 30       Then Do

36:  Z=F (P30)
 1: -999     F
 2: 0        Exponent of 10
 3: 37       Z Loc [ WR_mean   ]

37:  Z=F (P30)
 1: 0        F
 2: 0        Exponent of 10
 3: 38       Z Loc [ WR_std    ]

38:  Z=F (P30)
 1: -999     F
 2: 0        Exponent of 10
 3: 154      Z Loc [ WR_D1_WVT ]

39:  End (P95)

;--------------------------------------------------------------------
; Projekt 1: Berechnungen

  40:  If time is (P92)
   1: 0        Minutes (Seconds --) into a
   2: 10       Interval (same units as above)
   3: 10       Set Output Flag High (Flag 0)

  41:  Set Active Storage Area (P80)
   1: 3        Input Storage Area
   2: 31       Loc [ Lutmp_std ]

  42:  Standard Deviation (P82)^20629
   1: 5        Reps
   2: 21       Sample Loc [ Lutemp_C  ]

  43:  If time is (P92)
   1: 0        Minutes (Seconds --) into a
   2: 60       Interval (same units as above)
   3: 10       Set Output Flag High (Flag 0)

  44:  Set Active Storage Area (P80)
   1: 3        Input Storage Area
   2: 30       Loc [ rF_std    ]

  45:  Standard Deviation (P82)^14427
   1: 1        Reps
   2: 20       Sample Loc [ rF_Proz   ]

; Niederschlag [mm]
  46:  If (X<=>F) (P89)
   1: 16       X Loc [ K_Ns      ]
   2: 1        =
   3: 1        F
   4: 30       Then Do

    47:  Pulse (P3)
     1: 1        Reps
     2: 2        Pulse Channel 2
     3: 2        Switch Closure, All Counts
     4: 27       Loc [ Regen_mm  ]
     5: 0.2      Mult
     6: 0.0      Offset

  48:  Else (P94)

    49:  Z=F (P30)
     1: -999     F
     2: 00       Exponent of 10
     3: 27       Z Loc [ Regen_mm  ]

  50:  End (P95)

51:  Z=X (P31)
 1: 27       X Loc [ Regen_mm  ]
 2: 78       Z Loc [ regen10   ]

52:  Z=X (P31)
 1: 20       X Loc [ rF_Proz   ]
 2: 93       Z Loc [ rF10      ]

53:  Z=X (P31)
 1: 30       X Loc [ rF_std    ]
 2: 95       Z Loc [ rFstd10   ]

;------------------------------------------------------------------------------
;------------------------------------------------------------------------------
; Projekt 1: Speichern                                          alle 10 Minuten

54:  If (X<=>F) (P89)
 1: 2        X Loc [ Proj1_ID  ]
 2: 1        =
 3: 1        F
 4: 30       Then Do

  55:  If time is (P92)
   1: 0        Minutes (Seconds --) into a
   2: 10       Interval (same units as above)
   3: 10       Set Output Flag High (Flag 0)

  56:  Set Active Storage Area (P80)^6213
   1: 1        Final Storage Area 1
   2: 10       Array ID

  57:  Sample (P70)^12948
   1: 1        Reps
   2: 1        Loc [ Stat_ID   ]

  58:  Sample (P70)^15272
   1: 1        Reps
   2: 2        Loc [ Proj1_ID  ]

  59:  Real Time (P77)^4652
   1: 1220     Year,Day,Hour/Minute (midnight = 2400)

  60:  Average (P71)^14992
   1: 4        Reps
   2: 21       Loc [ Lutemp_C  ]

61:  Sample (P70)^20253
 1: 2        Reps
 2: 153      Loc [ WS_S_WVT  ]

  62:  Sample (P70)^3019
   1: 5        Reps
   2: 31       Loc [ Lutmp_std ]

  63:  Sample (P70)^28147
   1: 1        Reps
   2: 38       Loc [ WR_std    ]

64:  Sample (P70)^4187
 1: 1        Reps
 2: 152      Loc [ Wi_Spitze ]

65:  Average (P71)^4953
     1: 1        Reps
     2: 93       Loc [ rF10      ]

66:  Totalize (P72)^14771
     1: 1        Reps
     2: 78       Loc [ regen10   ]

67:  Sample (P70)^1299
     1: 1        Reps
     2: 95       Loc [ rFstd10   ]

;------------------------------------------------------------------------------
; Projekt 1: Speichern                                          alle 60 Minuten

  68:  If time is (P92)
   1: 0        Minutes (Seconds --) into a
   2: 60       Interval (same units as above)
   3: 10       Set Output Flag High (Flag 0)

  69:  Set Active Storage Area (P80)^2359
   1: 1        Final Storage Area 1
   2: 60       Array ID

  70:  Sample (P70)^3531
   1: 1        Reps
   2: 1        Loc [ Stat_ID   ]

  71:  Sample (P70)^112
   1: 1        Reps
   2: 2        Loc [ Proj1_ID  ]

  72:  Real Time (P77)^6963
   1: 1220     Year,Day,Hour/Minute (midnight = 2400)

  73:  Average (P71)^9597
   1: 1        Reps
   2: 20       Loc [ rF_Proz   ]

  74:  Totalize (P72)^822
   1: 1        Reps
   2: 27       Loc [ Regen_mm  ]

  75:  Sample (P70)^11972
   1: 1        Reps
   2: 30       Loc [ rF_std    ]

76:  End (P95)

;==============================================================================
;------------------------------------------------------------------------------
; Projekt 4: Speichern                                         alle 240 Minuten

  77:  If time is (P92)
   1: 0000     Minutes (Seconds --) into a
   2: 240      Interval (same units as above)
   3: 10       Set Output Flag High (Flag 0)

  78:  Set Active Storage Area (P80)^21536
   1: 2        Final Storage Area 2
   2: 240      Array ID

  79:  Sample (P70)^18649
   1: 1        Reps
   2: 1        Loc [ Stat_ID   ]

  80:  Sample (P70)^32552
   1: 1        Reps
   2: 5        Loc [ Proj4_ID  ]

  81:  Real Time (P77)^22461
   1: 1220     Year,Day,Hour/Minute (midnight = 2400)

  82:  Average (P71)^18299
   1: 2        Reps
   2: 90       Loc [ Batt_V    ]

83:  Average (P71)^1177
 1: 1        Reps
 2: 149      Loc [ LiBatt_V  ]

;==============================================================================
; Projekt 22: Speichern Echo EC-5 Bodenwassergehaltssensoren    alle 60 Minuten

84:  If (X<=>F) (P89)
 1: 9        X Loc [ Proj22_ID ]
 2: 1        =
 3: 22       F
 4: 30       Then Do

  85:  If time is (P92)
   1: 0000     Minutes (Seconds --) into a
   2: 60       Interval (same units as above)
   3: 10       Set Output Flag High (Flag 0)

; Array ID entspricht dem Messintervall in den Daten (erste Kolonne)
  86:  Set Active Storage Area (P80)^14610
   1: 1        Final Storage Area 1
   2: 60       Array ID

  87:  Sample (P70)^2551
   1: 1        Reps
   2: 1        Loc [ Stat_ID   ]

  88:  Sample (P70)^22664
   1: 1        Reps
   2: 9        Loc [ Proj22_ID ]

  89:  Real Time (P77)^24350
   1: 1220     Year,Day,Hour/Minute (midnight = 2400)

; Bodenwassergehalt [m3/m3], 12 Sensoren
  90:  Average (P71)^15721
   1: 12       Reps
   2: 160      Loc [ ECHO_1_15 ]


; Spannung [mV] (Rohwert) Bodenwasserghelatssensoren, 12 Sensoren
  91:  Average (P71)^21312
   1: 12       Reps
   2: 180      Loc [ ECHOU1_15 ]

92:  End (P95)

;==============================================================================
; Aufruf der Stationsprogramme

; Solarladeregelung
93:  Do (P86)
 1: 7        Call Subroutine 7

; Natel D Datenuebertragung
94:  Do (P86)
 1: 8        Call Subroutine 8

; SMS-Batteriewarnung
95:  If time is (P92)
 1: 1290     Minutes (Seconds --) into a
 2: 1440     Interval (same units as above)
 3: 30       Then Do

  96:  If (X<=>F) (P89)
   1: 90       X Loc [ Batt_V    ]
   2: 3        >=
   3: 11.5     F @@98
   4: 30       Then Do

    97:  If (X<=>F) (P89)
     1: 90       X Loc [ Batt_V    ]
     2: 4        <
     3: 12.0     F @@99
     4: 9        Call Subroutine 9

  98:  End (P95)

99:  End (P95)

;------------------------------------------------------------------------------
;==============================================================================
*Table 2 Program
  02: 10        Execution Interval (seconds)
;-------------------------------------------------------------------------------
; Projekt 1: Sensormessungen
; MP103A, SK1110, SKP215, SKU430, A100R, W200P, ARG100

; Lufttemperatur [Grad C], rel. Luftfeuchtigkeit [%]
  1:  If (X<=>F) (P89)
   1: 10       X Loc [ K_TrF     ]
   2: 1        =
   3: 1        F
   4: 30       Then Do

    2:  Do (P86)
     1: 46       Set Port 6 High

    3:  Excitation with Delay (P22)
     1: 3        Ex Channel
     2: 0        Delay W/Ex (units = 0.01 sec)
     3: 10       Delay After Ex (units = 0.01 sec)
     4: 0        mV Excitation

    4:  Volts (SE) (P1)
     1: 1        Reps
     2: 5        2500 mV Slow Range
     3: 2        SE Channel
     4: 20       Loc [ rF_Proz   ]
     5: 0.1      Mult
     6: 0        Offset

    5:  Volt (SE) (P1)
     1: 1        Reps
     2: 5        2500 mV Slow Range
     3: 3        SE Channel
     4: 21       Loc [ Lutemp_C  ]
     5: 0.1      Mult
     6: -40      Offset

    6:  Do (P86)
     1: 56       Set Port 6 Low

  7:  Else (P94)

    8:  Z=F (P30)
     1: -999     F
     2: 00       Exponent of 10
     3: 20       Z Loc [ rF_Proz   ]

    9:  Z=F (P30)
     1: -999     F
     2: 00       Exponent of 10
     3: 21       Z Loc [ Lutemp_C  ]

  10:  End (P95)

; Globalstrahlung [W/m2]
  11:  If (X<=>F) (P89)
   1: 11       X Loc [ K_Pyr     ]
   2: 1        =
   3: 1        F
   4: 30       Then Do

    12:  Volt (SE) (P1)
     1: 1        Reps
     2: 3        25 mV Slow Range
     3: 7        SE Channel
     4: 22       Loc [ GS_Wm2    ]
     5: 100.0    Mult
     6: 0.0      Offset

  13:  Else (P94)

    14:  Z=F (P30)
     1: -999     F
     2: 00       Exponent of 10
     3: 22       Z Loc [ GS_Wm2    ]

  15:  End (P95)

; PAR [umol/m2/s]
  16:  If (X<=>F) (P89)
   1: 12       X Loc [ K_PAR     ]
   2: 1        =
   3: 1        F
   4: 30       Then Do

    17:  Volt (SE) (P1)
     1: 1        Reps
     2: 3        25 mV Slow Range
     3: 8        SE Channel
     4: 23       Loc [ PAR_umol  ]
     5: 100.0    Mult
     6: 0.0      Offset

  18:  Else (P94)

    19:  Z=F (P30)
     1: -999     F
     2: 00       Exponent of 10
     3: 23       Z Loc [ PAR_umol  ]

  20:  End (P95)

; UVB [mW/m2]
  21:  If (X<=>F) (P89)
   1: 13       X Loc [ K_UV      ]
   2: 1        =
   3: 1        F
   4: 30       Then Do

    22:  Do (P86)
     1: 47       Set Port 7 High

    23:  Excitation with Delay (P22)
     1: 3        Ex Channel
     2: 0000     Delay W/Ex (units = 0.01 sec)
     3: 10       Delay After Ex (units = 0.01 sec)
     4: 0000     mV Excitation

    24:  Volt (Diff) (P2)
     1: 1        Reps
     2: 4        250 mV Slow Range
     3: 3        DIFF Channel
     4: 24       Loc [ UVB_mWm2  ]
     5: 1        Mult @@9
     6: 0.0      Offset

    25:  Do (P86)
     1: 57       Set Port 7 Low

  26:  Else (P94)

    27:  Z=F (P30)
     1: -999     F
     2: 00       Exponent of 10
     3: 24       Z Loc [ UVB_mWm2  ]

  28:  End (P95)

;==============================================================================
; Projekt 4: Service

29:  If (X<=>F) (P89)
 1: 5        X Loc [ Proj4_ID  ]
 2: 2        <>
 3: 0        F
 4: 30       Then Do

  30:  Batt Voltage (P10)
   1: 90       Loc [ Batt_V    ]

  31:  Internal Temperature (P17)
   1: 91       Loc [ CR10Tmp_C ]

32:  Status Monitor (P130)
 1: 3        Lithium Battery (volts)
 2: 149      Loc [ LiBatt_V  ]

33:  End (P95)

;------------------------------------------------------------------------------
;==========================================================================
; Projekt 22: Echo EC-5 Bodenwassergehaltssensoren

34:  If (X<=>F) (P89)
 1: 9        X Loc [ Proj22_ID ]
 2: 2        <>
 3: 0        F
 4: 30       Then Do

; Alle 10 Minuten messen
    35:  If time is (P92)
     1: 59       Minutes (Seconds --) into a
     2: 60       Interval (same units as above)
     3: 30       Then Do

;     AM416 aktivieren, weiterschalgen (Set1 bis Set11)
      36:  Do (P86)
       1: 41       Set Port 1 High

      37:  Beginning of Loop (P87)
       1: 0        Delay
       2: 11       Loop Count

        38:  Do (P86)
         1: 72       Pulse Port 2

        39:  Excitation with Delay (P22)
         1: 1        Ex Channel
         2: 0000     Delay W/Ex (units = 0.01 sec)
         3: 1        Delay After Ex (units = 0.01 sec)
         4: 0000     mV Excitation


      40:  End (P95)

;     Messen Spannung Echo EC-5 Bodenwassergehaltssensoren (Set12-14)
      41:  Beginning of Loop (P87)
       1: 0        Delay
       2: 3        Loop Count

        42:  Do (P86)
         1: 72       Pulse Port 2

        43:  Excitation with Delay (P22)
         1: 1        Ex Channel
         2: 0000     Delay W/Ex (units = 0.01 sec)
         3: 1        Delay After Ex (units = 0.01 sec)
         4: 0000     mV Excitation


        44:  Step Loop Index (P90)
         1: 3        Step

        45:  Excite-Delay (SE) (P4)
         1: 1        Reps
         2: 5        2500 mV Slow Range
         3: 9        SE Channel
         4: 1        Excite all reps w/Exchan 1
         5: 1        Delay (0.01 sec units)
         6: 2500     mV Excitation
         7: 180   -- Loc [ ECHOU1_15 ]
         8: 1        Multiplier
         9: 0        Offset

        46:  Excite-Delay (SE) (P4)
         1: 1        Reps
         2: 5        2500 mV Slow Range
         3: 10       SE Channel
         4: 1        Excite all reps w/Exchan 1
         5: 1        Delay (0.01 sec units)
         6: 2500     mV Excitation
         7: 181   -- Loc [ ECHOU1_50 ]
         8: 1        Multiplier
         9: 0        Offset

        47:  Excite-Delay (SE) (P4)
         1: 1        Reps
         2: 5        2500 mV Slow Range
         3: 11       SE Channel
         4: 1        Excite all reps w/Exchan 1
         5: 1        Delay (0.01 sec units)
         6: 2500     mV Excitation
         7: 182   -- Loc [ ECHOU1_80 ]
         8: 1        Multiplier
         9: 0        Offset


      48:  End (P95)

; AM416 deaktivieren
      49:  Do (P86)
       1: 51       Set Port 1 Low


; Umrechung in Bodenwassergehalt (Lineare Kalibration pro Sensor)
; Standardkalibration Y=X*0.00119-0.4 fuer Mineralboeden (p., 17 in Manual, 2006)

; Echo_1_15
      50:  Z=X*F (P37)
       1: 180      X Loc [ ECHOU1_15 ]
       2: 0.00119  F
       3: 160      Z Loc [ ECHO_1_15 ]

      51:  Z=X+F (P34)
       1: 160      X Loc [ ECHO_1_15 ]
       2: -.4      F
       3: 160      Z Loc [ ECHO_1_15 ]

; Echo_1_50
      52:  Z=X*F (P37)
       1: 181      X Loc [ ECHOU1_50 ]
       2: 0.00119  F
       3: 161      Z Loc [ ECHO_1_50 ]

      53:  Z=X+F (P34)
       1: 161      X Loc [ ECHO_1_50 ]
       2: -.4      F
       3: 161      Z Loc [ ECHO_1_50 ]

; Echo_1_80
      54:  Z=X*F (P37)
       1: 182      X Loc [ ECHOU1_80 ]
       2: 0.00119  F
       3: 162      Z Loc [ ECHO_1_80 ]

      55:  Z=X+F (P34)
       1: 162      X Loc [ ECHO_1_80 ]
       2: -.4      F
       3: 162      Z Loc [ ECHO_1_80 ]

; Echo_2_15
      56:  Z=X*F (P37)
       1: 183      X Loc [ ECHOU2_15 ]
       2: 0.00119  F
       3: 163      Z Loc [ ECHO_2_15 ]

      57:  Z=X+F (P34)
       1: 163      X Loc [ ECHO_2_15 ]
       2: -.4      F
       3: 163      Z Loc [ ECHO_2_15 ]

; Echo_2_50
      58:  Z=X*F (P37)
       1: 184      X Loc [ ECHOU2_50 ]
       2: 0.00119  F
       3: 164      Z Loc [ ECHO_2_50 ]

      59:  Z=X+F (P34)
       1: 164      X Loc [ ECHO_2_50 ]
       2: -.4      F
       3: 164      Z Loc [ ECHO_2_50 ]

; Echo_2_80
      60:  Z=X*F (P37)
       1: 185      X Loc [ ECHOU2_80 ]
       2: 0.00119  F
       3: 165      Z Loc [ ECHO_2_80 ]

      61:  Z=X+F (P34)
       1: 165      X Loc [ ECHO_2_80 ]
       2: -.4      F
       3: 165      Z Loc [ ECHO_2_80 ]


; Echo_3_15
      62:  Z=X*F (P37)
       1: 186      X Loc [ ECHOU3_15 ]
       2: 0.00119  F
       3: 166      Z Loc [ ECHO_3_15 ]

      63:  Z=X+F (P34)
       1: 166      X Loc [ ECHO_3_15 ]
       2: -.4      F
       3: 166      Z Loc [ ECHO_3_15 ]

; Echo_3_50
      64:  Z=X*F (P37)
       1: 187      X Loc [ ECHOU3_50 ]
       2: 0.00119  F
       3: 167      Z Loc [ ECHO_3_50 ]

      65:  Z=X+F (P34)
       1: 167      X Loc [ ECHO_3_50 ]
       2: -.4      F
       3: 167      Z Loc [ ECHO_3_50 ]

; Echo_3_80
      66:  Z=X*F (P37)
       1: 188      X Loc [ ECHOU3_80 ]
       2: 0.00119  F
       3: 168      Z Loc [ ECHO_3_80 ]

      67:  Z=X+F (P34)
       1: 168      X Loc [ ECHO_3_80 ]
       2: -.4      F
       3: 168      Z Loc [ ECHO_3_80 ]


    68:  End (P95)


    69:  End (P95)

*Table 3 Subroutines
;------------------------------------------------------------------------------
; Stationsprogramm 7

1:  Beginning of Subroutine (P85)
 1: 7        Subroutine 7

; Korrektur fuer Ladespannung
  2:  Z=X*F (P37)
   1: 91       X Loc [ CR10Tmp_C ]
   2: -0.03    F
   3: 128      Z Loc [ korBatt_V ]

; Sollreduktion wenn Shunt ein
  3:  If Flag/Port (P91)
   1: 44       Do if Port 4 is High
   2: 30       Then Do

    4:  Z=X+F (P34)
     1: 128      X Loc [ korBatt_V ]
     2: -0.6     F
     3: 128      Z Loc [ korBatt_V ]

  5:  End (P95)

; Hysterese kompensieren
  6:  Z=X+F (P34)
   1: 128      X Loc [ korBatt_V ]
   2: 14.2     F
   3: 129      Z Loc [ Ladsoll_V ]

; Limitierung der Ladespannung
  7:  If (X<=>F) (P89)
   1: 129      X Loc [ Ladsoll_V ]
   2: 3        >=
   3: 14       F
   4: 30       Then Do

    8:  Z=F (P30)
     1: 14       F
     2: 0        Exponent of 10
     3: 129      Z Loc [ Ladsoll_V ]

  9:  End (P95)

; erzwungene Batterieentladung ueber Flag 1
  10:  If Flag/Port (P91)
   1: 11       Do if Flag 1 is High
   2: 30       Then Do

    11:  Do (P86)
     1: 44       Set Port 4 High

; erzwungene Entladung waehrend max. 4 Stunden
    12:  If time is (P92)
     1: 0        Minutes (Seconds --) into a
     2: 240      Interval (same units as above)
     3: 21       Set Flag 1 Low

  13:  Else (P94)

; Shunt ein wenn Batt_V >= Ladsoll_V
    14:  IF (X<=>Y) (P88)
     1: 90       X Loc [ Batt_V    ]
     2: 3        >=
     3: 129      Y Loc [ Ladsoll_V ]
     4: 30       Then Do

      15:  Do (P86)
       1: 44       Set Port 4 High

    16:  Else (P94)

; Shunt aus wenn Batt_V < Ladsoll_V
      17:  Do (P86)
       1: 54       Set Port 4 Low

    18:  End (P95)

  19:  End (P95)

20:  End (P95)

;------------------------------------------------------------------------------
; Stationsprogramm 8

21:  Beginning of Subroutine (P85)
 1: 8        Subroutine 8

  22:  If time is (P92)
   1: 0        Minutes (Seconds --) into a
   2: 1        Interval (same units as above)
   3: 10       Set Output Flag High (Flag 0)

  23:  Set Active Storage Area (P80)
   1: 3        Input Storage Area
   2: 138      Loc [ Zeit_HHMM ]

  24:  Real Time (P77)^4440
   1: 20       Hour/Minute (midnight = 2400)

;-----------------------------------------------------------------
;GSM-Zeitfenster
  25:  IF (X<=>F) (P89)
   1: 138      X Loc [ Zeit_HHMM ]
   2: 3        >=
   3: 530      F @@92
   4: 12       Set Flag 2 High

  26:  IF (X<=>F) (P89)
   1: 138      X Loc [ Zeit_HHMM ]
   2: 3        >=
   3: 545      F @@93
   4: 22       Set Flag 2 Low

  27:  IF (X<=>F) (P89)
   1: 138      X Loc [ Zeit_HHMM ]
   2: 3        >=
   3: 1000     F @@90
   4: 12       Set Flag 2 High

  28:  IF (X<=>F) (P89)
   1: 138      X Loc [ Zeit_HHMM ]
   2: 3        >=
   3: 1015     F @@91
   4: 22       Set Flag 2 Low

;--------------------------------------------------------------
;GSM-Service-Fenster

29:  If (X<=>F) (P89)
 1: 138      X Loc [ Zeit_HHMM ]
 2: 3        >=
 3: 1000     F
 4: 30       Then Do

30:  If (X<=>F) (P89)
 1: 90       X Loc [ Batt_V    ]
 2: 3        >=
 3: 12.8     F
 4: 12       Set Flag 2 High

31:  End (P95)

32:  If (X<=>F) (P89)
 1: 138      X Loc [ Zeit_HHMM ]
 2: 3        >=
 3: 1330     F
 4: 30       Then Do

33:  If (X<=>F) (P89)
 1: 90       X Loc [ Batt_V    ]
 2: 3        >=
 3: 12.6     F
 4: 12       Set Flag 2 High

34:  End (P95)

35:  If (X<=>F) (P89)
 1: 138      X Loc [ Zeit_HHMM ]
 2: 3        >=
 3: 1430     F
 4: 22       Set Flag 2 Low

;--------------------------------------------------------------
; Kamera und Modem ein Fenster 1

36:  If (X<=>F) (P89)
 1: 138      X Loc [ Zeit_HHMM ]
 2: 3        >=
 3: 0657     F
 4: 30       Then Do

37:  If (X<=>F) (P89)
 1: 90       X Loc [ Batt_V    ]
 2: 3        >=
 3: 12.8     F
 4: 48       Set Port 8 High

38:  End (P95)

39:  If (X<=>F) (P89)
 1: 138      X Loc [ Zeit_HHMM ]
 2: 3        >=
 3: 0702     F
 4: 58       Set Port 8 Low

;--------------------------------------------------------------
; Kamera und Modem ein Fenster 2

40:  If (X<=>F) (P89)
 1: 138      X Loc [ Zeit_HHMM ]
 2: 3        >=
 3: 1057     F
 4: 30       Then Do

41:  If (X<=>F) (P89)
 1: 90       X Loc [ Batt_V    ]
 2: 3        >=
 3: 12.8     F
 4: 48       Set Port 8 High

42:  End (P95)

43:  If (X<=>F) (P89)
 1: 138      X Loc [ Zeit_HHMM ]
 2: 3        >=
 3: 1102     F
 4: 58       Set Port 8 Low

;--------------------------------------------------------------
; Kamera und Modem ein Fenster 3

44:  If (X<=>F) (P89)
 1: 138      X Loc [ Zeit_HHMM ]
 2: 3        >=
 3: 1547     F
 4: 30       Then Do

45:  If (X<=>F) (P89)
 1: 90       X Loc [ Batt_V    ]
 2: 3        >=
 3: 12.3     F
 4: 48       Set Port 8 High

46:  End (P95)

47:  If (X<=>F) (P89)
 1: 138      X Loc [ Zeit_HHMM ]
 2: 3        >=
 3: 1552     F
 4: 58       Set Port 8 Low

;--------------------------------------------------------------
; Kamera und Modem ein Fenster 4

48:  If (X<=>F) (P89)
 1: 138      X Loc [ Zeit_HHMM ]
 2: 3        >=
 3: 1557     F
 4: 30       Then Do

49:  If (X<=>F) (P89)
 1: 90       X Loc [ Batt_V    ]
 2: 3        >=
 3: 12.8     F
 4: 48       Set Port 8 High ;

50:  End (P95)

51:  If (X<=>F) (P89)
 1: 138      X Loc [ Zeit_HHMM ]
 2: 3        >=
 3: 1602     F
 4: 58       Set Port 8 Low

;--------------------------------------------------------------
; Schalter fuer temporaeren GSM-Einsatz 30 Minuten
52:  If Flag/Port (P91)
 1: 22       Do if Flag 2 is Low
 2: 30       Then Do

53:  If Flag/Port (P91)
 1: 53       Do if Port 3 is Low
 2: 30       Then Do

54:  Do (P86)
 1: 27       Set Flag 7 Low

55:  Do (P86)
 1: 28       Set Flag 8 Low

56:  If Flag/Port (P91)
 1: 28       Do if Flag 8 is Low
 2: 30       Then Do

57:  Z=F (P30)
 1: 0        F
 2: 0        Exponent of 10
 3: 150      Z Loc [ ZahlerMin ]

58:  End (P95)

59:  End (P95)

60:  If Flag/Port (P91)
 1: 43       Do if Port 3 is High
 2: 17       Set Flag 7 High

61:  If Flag/Port (P91)
 1: 17       Do if Flag 7 is High
 2: 30       Then Do

62:  If Flag/Port (P91)
 1: 28       Do if Flag 8 is Low
 2: 30       Then Do

63:  Z=F (P30)
 1: -30      F
 2: 0        Exponent of 10
 3: 150      Z Loc [ ZahlerMin ]

64:  Do (P86)
 1: 18       Set Flag 8 High

65:  End (P95)

66:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 1        Interval (same units as above)
 3: 30       Then Do

67:  Z=Z+1 (P32)
 1: 150      Z Loc [ ZahlerMin ]

68:  End (P95)

69:  If (X<=>F) (P89)
 1: 150      X Loc [ ZahlerMin ]
 2: 3        >=
 3: -60      F
 4: 45       Set Port 5 High

70:  If (X<=>F) (P89)
 1: 150      X Loc [ ZahlerMin ]
 2: 3        >=
 3: 0        F
 4: 55       Set Port 5 Low

71:  End (P95)

72:  End (P95)

73:  End (P95)


;------------------------------------------------------------------------------
; Stationsprogramm 9

74:  Beginning of Subroutine (P85)
 1: 9        Subroutine 9

  75:  Initiate Telecommunications (P97)
   1: 72       Generic Modem/9600 Baud
   2: 06       Disabled when User Flag 6 is High
   3: 20       Seconds Call Time Limit
   4: 60       Seconds Before Fast Retry
   5: 2        Fast Retries
   6: 0000     Minutes before Slow Retry
   7: 148      Failures Loc [ SMS_Fail  ]
   8: 0000     Call-back ID

  76:  Extended Parameters 4 Digit (P68)
   1: 87       Option  ; W
   2: 10       Option  ; ^J
   3: 69       Option  ; E
   4: 10       Option  ; ^J
   5: 65       Option  ; A
   6: 84       Option  ; T
   7: 43       Option  ; +
   8: 67       Option  ; C

  77:  Extended Parameters 4 Digit (P68)
   1: 77       Option  ; M
   2: 71       Option  ; G
   3: 70       Option  ; F
   4: 61       Option  ; =
   5: 49       Option  ; 1
   6: 13       Option  ; ^M
   7: 87       Option  ; W
   8: 20       Option  ; ^T

  78:  Extended Parameters 4 Digit (P68)
   1: 87       Option  ; W
   2: 10       Option  ; ^J
   3: 69       Option  ; E
   4: 23       Option  ; ^W
   5: 65       Option  ; A
   6: 84       Option  ; T
   7: 43       Option  ; +
   8: 67       Option  ; C

  79:  Extended Parameters 4 Digit (P68)
   1: 77       Option  ; M
   2: 71       Option  ; G
   3: 83       Option  ; S
   4: 61       Option  ; =
   5: 34       Option  ; "
   6: 43       Option  ; +
   7: 52       Option  ; 4
   8: 49       Option  ; 1

  80:  Extended Parameters 4 Digit (P68)
   1: 55       Option  ; 7
   2: 57       Option  ; 9
   3: 50       Option  ; 2
   4: 48       Option  ; 0
   5: 56       Option  ; 8
   6: 51       Option  ; 3
   7: 54       Option  ; 6
   8: 50       Option  ; 2

  81:  Extended Parameters 4 Digit (P68)
   1: 55       Option  ; 7
   2: 34       Option  ; "
   3: 13       Option  ; ^M
   4: 82       Option  ; R
   5: 3        Option  ; ^C
   6: 10       Option  ; ^J
   7: 62       Option  ; >
   8: 32       Option  ; Space

  82:  Extended Parameters 4 Digit (P68)
   1: 69       Option  ; E
   2: 17       Option  ; ^Q
   3: 66       Option  ; B
   4: 97       Option  ; a
   5: 116      Option  ; t
   6: 116      Option  ; t
   7: 101      Option  ; e
   8: 114      Option  ; r

  83:  Extended Parameters 4 Digit (P68)
   1: 105      Option  ; i
   2: 101      Option  ; e
   3: 32       Option  ; Space
   4: 76       Option  ; L
   5: 87       Option  ; W
   6: 70       Option  ; F
   7: 50       Option  ; 2
   8: 56       Option  ; 8

  84:  Extended Parameters 4 Digit (P68)
   1: 32       Option  ; Space
   2: 61       Option  ; =
   3: 32       Option  ; Space
   4: 73       Option  ; I
   5: 90       Option  ; Z
   6: 69       Option  ; E
   7: 2        Option  ; ^B
   8: 86       Option  ; V

  85:  Extended Parameters 4 Digit (P68)
   1: 26       Option  ; ^2
   2: 68       Option  ; D
   3: 100      Option  ; d
   4: 0000     Option
   5: 0000     Option
   6: 0000     Option
   7: 0000     Option
   8: 0000     Option

86:  End (P95)
;------------------------------------------------------------------------------
End Program














-Input Locations-
1 Stat_ID   1 4 1
2 Proj1_ID  1 4 1
3 Proj2_ID  1 0 1
4 Proj3_ID  1 0 1
5 Proj4_ID  1 2 1
6 Proj5_ID  1 0 1
7 Proj6_ID  1 0 1
8 Proj7_ID  1 0 1
9 Proj22_ID 1 3 1
10 K_TrF     1 1 0
11 K_Pyr     1 1 1
12 K_PAR     1 1 1
13 K_UV      1 1 1
14 K_WG      1 1 1
15 K_WR      1 2 1
16 K_Ns      1 1 1
17 _________ 0 0 0
18 _________ 0 0 0
19 _________ 0 0 0
20 rF_Proz   1 3 2
21 Lutemp_C  1 2 2
22 GS_Wm2    1 2 2
23 PAR_umol  1 2 2
24 UVB_mWm2  1 2 2
25 WS_ms     1 4 2
26 WR_Grad   1 2 2
27 Regen_mm  1 2 2
28 _________ 0 0 0
29 _________ 0 0 0
30 rF_std    1 3 1
31 Lutmp_std 1 1 1
32 GS_std    9 1 0
33 PAR_std   9 1 0
34 UVB_std   9 1 0
35 WS_std    17 1 0
36 WS_mean   1 0 1
37 WR_mean   1 0 1
38 WR_std    1 1 1
39 K_Dendr10 1 0 0
40 K_Dendr01 1 0 0
41 K_Drehp1  1 0 0
42 K_Dendr02 1 0 0
43 K_Dendr03 1 0 0
44 K_Dendr04 1 0 0
45 K_Dendr05 1 0 0
46 K_Dendr06 1 0 0
47 K_Dendr07 1 0 0
48 K_Dendr08 1 0 0
49 K_Dendr09 1 0 0
50 Dendro01  1 0 0
51 Dendro02  1 0 0
52 Dendro03  1 0 0
53 Dendro04  1 0 0
54 Dendro05  1 0 0
55 Dendro06  1 0 0
56 Dendro07  1 0 0
57 Dendro08  1 0 0
58 Dendro09  1 0 0
59 Dendro10  1 0 0
60 Drehpot1  1 0 0
61 Drehpot2  1 0 0
62 Drehpot3  1 0 0
63 Drehpot4  1 0 0
64 K_Drehp2  1 0 0
65 K_Drehp3  1 0 0
66 K_Drehp4  1 0 0
67 _________ 1 0 0
68 _________ 0 0 0
69 _________ 0 0 0
70 K_CS505_1 1 0 0
71 K_CS205_1 1 1 0
72 K_CS505_2 1 0 0
73 K_CS205_2 1 0 0
74 _________ 0 0 0
75 _________ 0 0 0
76 _________ 0 0 0
77 _________ 0 0 0
78 regen10   1 1 1
79 _________ 0 0 0
80 pCS505_1  1 0 0
81 pCS505_2  1 0 0
82 CS205_1   5 0 1
83 CS205_2   17 0 1
84 CS505_1   1 0 0
85 CS505_2   1 0 0
86 _________ 1 0 0
87 _________ 1 0 0
88 _________ 0 0 0
89 _________ 0 0 0
90 Batt_V    1 10 1
91 CR10Tmp_C 1 2 1
92 _________ 0 0 0
93 rF10      1 1 1
94 _________ 0 0 0
95 rFstd10   1 1 1
96 _________ 0 0 0
97 K_ST1_C   1 0 0
98 K_ST2_C   1 0 0
99 K_SF1_pro 1 0 0
100 K_SF2_pro 1 0 0
101 K_BT1_5   1 0 0
102 K_BT2_5   1 0 0
103 K_BT1_10  1 0 0
104 K_BT2_10  1 0 0
105 K_BT1_20  1 0 0
106 K_BT2_20  1 0 0
107 _________ 0 0 0
108 _________ 0 0 0
109 _________ 0 0 0
110 ST1_C     1 0 0
111 SF1_proz  1 0 0
112 ST2_C     1 0 0
113 SF2_proz  1 0 0
114 BT1_5     1 0 0
115 BT2_5     1 0 0
116 BT1_10    1 0 0
117 BT2_10    1 0 0
118 BT1_20    1 0 0
119 BT2_20    1 0 0
120 _________ 1 0 0
121 _________ 1 0 0
122 _________ 0 0 0
123 _________ 0 0 0
124 _________ 0 0 0
125 _________ 0 0 0
126 _________ 0 0 0
127 _________ 0 0 0
128 korBatt_V 1 2 2
129 Ladsoll_V 1 2 2
130 _________ 0 0 0
131 _________ 0 0 0
132 _________ 1 0 0
133 _________ 1 0 0
134 _________ 1 0 0
135 _________ 1 0 0
138 Zeit_HHMM 1 15 1
148 SMS_Fail  1 0 1
149 LiBatt_V  1 1 1
150 ZahlerMin 1 2 3
151 Dendro061 1 0 0
152 Wi_Spitze 1 1 1
153 WS_S_WVT  1 1 0
154 WR_D1_WVT 1 1 1
160 ECHO_1_15 7 2 2
161 ECHO_1_50 11 2 2
162 ECHO_1_80 11 2 2
163 ECHO_2_15 11 2 2
164 ECHO_2_50 11 2 2
165 ECHO_2_80 11 2 2
166 ECHO_3_15 11 2 2
167 ECHO_3_50 11 2 2
168 ECHO_3_80 19 2 2
180 ECHOU1_15 7 2 1
181 ECHOU1_50 11 2 1
182 ECHOU1_80 11 2 1
183 ECHOU2_15 11 2 0
184 ECHOU2_50 11 2 0
185 ECHOU2_80 11 2 0
186 ECHOU3_15 11 2 0
187 ECHOU3_50 11 2 0
188 ECHOU3_80 19 2 0
189 _________ 1 1 0
-Program Security-
9044
9045
0000
-Mode 4-
1
2
3
4
5
6
7
8
9
10
11
12
13
14
15
16
22
90
91
92
93
98
99
-Final Storage Area 2-
1024
-CR10X ID-
0
-CR10X Power Up-
3
-CR10X Compile Setting-
3
-CR10X RS-232 Setting-
-1
-DLD File Labels-
0
-Final Storage Labels-
0,WS_ms_S_WVT~25,15582
0,WR_Grad_D1_WVT~26
0,WR_Grad_SD1_WVT~26
1,WS_ms_MAX~25,21844
2,WS_ms_S_WVT~25,21398
2,WR_Grad_D1_WVT~26
3,Lutemp_C_STD~21,20629
3,GS_Wm2_STD~22
3,PAR_umol_STD~23
3,UVB_mWm2_STD~24
3,WS_ms_STD~25
4,rF_Proz_STD~20,14427
5,10,6213
6,Stat_ID~1,12948
7,Proj1_ID~2,15272
8,Year_RTM,4652
8,Day_RTM
8,Hour_Minute_RTM
9,Lutemp_C_AVG~21,14992
9,GS_Wm2_AVG~22
9,PAR_umol_AVG~23
9,UVB_mWm2_AVG~24
10,WS_S_WVT~153,20253
10,WR_D1_WVT~154
11,Lutmp_std~31,3019
11,GS_std~32
11,PAR_std~33
11,UVB_std~34
11,WS_std~35
12,WR_std~38,28147
13,Wi_Spitze~152,4187
14,rF10_AVG~93,4953
15,regen10_TOT~78,14771
16,rFstd10~95,1299
17,Stat_ID~1,3531
18,Proj1_ID~2,112
19,Year_RTM,6963
19,Day_RTM
19,Hour_Minute_RTM
20,rF_Proz_AVG~20,9597
21,Regen_mm_TOT~27,822
22,rF_std~30,11972
23,240,21536
24,Stat_ID~1,18649
25,Proj4_ID~5,32552
26,Year_RTM,22461
26,Day_RTM
26,Hour_Minute_RTM
27,Batt_V_AVG~90,18299
27,CR10Tmp_C_AVG~91
28,LiBatt_V_AVG~149,1177
29,Stat_ID~1,2551
30,Proj22_ID~9,22664
31,Year_RTM,24350
31,Day_RTM
31,Hour_Minute_RTM
32,ECHO_1_15_AVG~160,15721
32,ECHO_1_50_AVG~161
32,ECHO_1_80_AVG~162
32,ECHO_2_15_AVG~163
32,ECHO_2_50_AVG~164
32,ECHO_2_80_AVG~165
32,ECHO_3_15_AVG~166
32,ECHO_3_50_AVG~167
32,ECHO_3_80_AVG~168
32,__________AVG~169
32,__________AVG~170
32,__________AVG~171
33,ECHOU1_15_AVG~180,21312
33,ECHOU1_50_AVG~181
33,ECHOU1_80_AVG~182
33,ECHOU2_15_AVG~183
33,ECHOU2_50_AVG~184
33,ECHOU2_80_AVG~185
33,ECHOU3_15_AVG~186
33,ECHOU3_50_AVG~187
33,ECHOU3_80_AVG~188
33,__________AVG~189
33,__________AVG~190
33,__________AVG~191
34,60,2359
35,Hour_Minute_RTM,4440
36,60,14610
