'**************************************************************************************
'    Peter Waldner, 5.9.2014
'    Aenderungen fuer in Betriebenahme als LWF Meteostation UIT_B
'    - StationsID: 
'    - Projektnummer: 1
'    Aenderungen fuer Anhaengen EC-5, 10HS und MPS2/MPS6 Sensoren

'**************************************************************************************
'*                                                                                    *
'*   Programm:          WSL_LWF_K11.CS1                                               *
'*                      Langfristige Waldökosystem-Forschung LWF / WSL                *
'*   Logger:            Campbell CR1000                                               *
'*   Version:           V 1.1                                                         *
'*   Ersterstellung:    12.04.2012                                                    *
'*   letzte Aenderung:  05.04.2013                                                    *
'*   Autor:             Martin Zimmerli, SensAlpin GmbH                               *
'*                                                                                    *
'**************************************************************************************
'*
'*   Station:                WSL LWF-Station UIT_B 
'*   Stations-ID:            1
'*   Telefonnummer:          079 179 90 64
'*   PakBus-Adresse:         101
'*
'*
'*   Messungen:
'*   **********
'*   Lufttemperatur          TAIR [°C], Rotronic Hygroclip HC 2            (SE3, C6)
'*   rel. Feuchte            RHUM [%],  Rotronic Hygroclip HC 2            (SE2, C6)
'*   Windrichtung            DWND [deg], Campbell W200P                   (SE4, Ex2)
'*   Windgeschwindigkeit     VWND [m/s], Campbell A100R                         (P1)
'*   Niederschlag            PLUV [mm], Campbell ARG100 0.2mm Wippe             (P2)
'*   Globalstrahlung         SRAD [W/m2], Skye SKS1110                         (SE7)
'*   photosynth. akt. Str.   PAR  [umol/m2s], Skye SKP 210/I                   (SE8)
'*   UVB-Strahlung           UVB  [mW/m2], Skye SKU 430/I                   (Diff 3)
'*
'*   Multiplexer             Mux 1: AM16/32_1                (C1, C2 / SE9 ... SE12)
'*
'*                           Mux 2: AM16/32_2               (C3, C4 / SE13 ... SE16)
'*
'*   Batteriespannung        VBAT [V]
'*   Loggertermperatur       TLOG [°C]
'*
'*
'*   INPUT CHANNELS:
'*   --------------
'*   SE1:  -           SE2:  RHUM        SE3:  TAIR        SE4:  DWND
'*   SE5:  UVB High    SE6:  UVB Low     SE7:  SRAD        SE8:  PAR   
'*   SE9:  Odd H Mux1  SE10: Odd L Mux1  SE11: Even H Mux1 SE12: Even L Mux1
'*   SE13: Odd H Mux2  SE14: Odd L Mux2  SE15: Even H Mux2 SE16: Even L Mux2
'*   
'*   PULSE INPUT CHANNELS:
'*   --------------------
'*   P1:  VWND         P2:  PLUV
'*
'*   CONTROL PORTS:
'*   -------------
'*   C1:  CLK Mux 1    C2:  RES Mux 1    C3:  CLK Mux 2    C4:  RES Mux 2   
'*   C5:  -            C6:  PS TARH      C7:  -            C8:  -
'*
'*   Switched 12V:     GSM  (low = on)
'*
'*   EXCITATION CHANNELS:
'*   -------------------
'*   Ex1: -            Ex2: DWND          Ex3: -   
'*
'*
'*   Programmkonfiguration:
'*   **********************
'*   Station:               Kennung durch StationID
'*   Sensoren:              Ein-/Aus-Schalten der Messung von XXXX mit StatXXXX
'*                          - StatXXXX >= 1:   eingeschaltet
'*                          - StatXXXX  = 0:   ausgeschaltet
'*   Niederschlag:          PLUVcali:  Wippenvolumen in mm
'*   Globalstrahlung:       SRADcali:  1 / Empfindlichkeit [mV/W/m2]
'*   phot. akt. Strahlung:  PARcali:   1 / Empfindlichkeit [mV/umol/m2s]
'*   UVB-Strahlung:         UVBcali:   1000 / Empfindlichkeit [mV/W/m2]
'*
'*   
'*   Energieversorgung:
'*   ******************
'*   Solarspeisung 15W / 38Ah mit Laderegler SunSaver SS-6
'*
'*   Kommunikation / Datenablage:
'*   ****************************
'*   GSM:                   Cinterion MC52i Terminal
'*                          geschaltet ueber SW12V (low: EIN / high: AUS) 
'*                          im Normalbetrieb immer ein / Reset taeglich um 11:55 Uhr
'*
'*                          bei tiefer Batteriespannung morgens um 05:00 Uhr
'*                          VBAT5h < VBATlim (default: 12.4V), wird das GSM-Modem
'*                          jeweils nur noch waehrend vier Zeitfenstern eingeschaltet
'*                          (default: 06:00-06:30/10:00-10:30/14:00-14:30/18:00-18:30)
'*
'*   GPRS:                  falls StatFTP >= 1:
'*                          GPRS-Verbindung wird nach dem jeweils ersten FTP-Transfer
'*                          nach jeder vollen Stunde einmal stuendlich geschlossen und
'*                          vor dem naechsten FTP-Transfer wieder geoeffnet.
'*                          falls StatFTP = 0:
'*                          GPRS-Verbindung wird stuendlich 
'*                          um hh:10 geoeffnet und
'*                          um hh:00 geschlossen.
'*
'*   FTP:                   falls StatFTP >= 1:
'*                          Alle 10 (cMETEOINT) Minuten wird eine Datenzeile ins 
'*                          lokale Datenfile USR:LocalData.dat geschrieben. 
'*                          Dieses wird per FTP auf einen FTP-Server uebertragen.
'*                          Bei erfolgreicher Uebertragung wird das File geloescht,
'*                          ansonsten wird es stehen gelassen.
'*
'*   Zeit:                  falls StatNTP >= 1:
'*                          Die Loggerzeit wird einmal taeglich nach Aufbau der 
'*                          ersten GPRS-Verbindung nach Mitternacht mit dem NTP-Server
'*                          "pool.ntp.org" auf GMT+1 ohne Sommerzeit synchronisiert.
'*          
'*   Daten:				          Meteodaten (cMETEOINT = 10 Minuten):
'*								          Stations-ID, Lufttemperatur, Luftfeuchtigkeit,
'*                          Windgeschwindigkeit, Windrichtung, Windspitze, 
'*                          Niederschlag, Niederschlagssumme, Globalstrahlung, 
'*                          photosynthetisch aktive Strahlung, UVB-Strahlung
'*                          Batteriespannung, Loggertemperatur, Qualitaetsparameter
'*
'*								          Konfigurationsdaten (bei Aenderung der Konfiguration):
'*								          Stations-ID, Status, PLUVcali, SRADcali, 
'*                          PARCali, UVBCali
'*                          
'*   Konfiguration:         Die Konfigurationsparameter werden in einem Text-File
'*                          auf dem USR-Drive abgelegt ("USR:Parameter.cal").
'*                          Bei jedem Neustart des Loggers werden die Parameter aus
'*                          diesem File neu eingelesen. Ist das File nicht vorhanden,
'*                          so werden die Default-Werte aus SUBDefaultParameter 
'*                          uebernommen.
'*                          Nach jeder Aenderung der Konfigurationsparameter sollten
'*                          diese durch Setzen von FlagEnterConfig neu abgelegt werden.
'*
'*                          Jedesmal, wenn das Kalibrationsfile neu geschrieben wird,
'*                          wird zur Dokumentation der aktuellen Konfiguration eine
'*                          Zeile im Table der Konfigurationsdaten abgelegt.
'*
'*
'**************************************************************************************
'*                                                                                    *
'*   D E K L A R A T I O N                                                            *
'*                                                                                    *
'**************************************************************************************
SequentialMode

'--------------------------------------------------------------------------------------
'-   K O N S T A N T E N                                                              -
'--------------------------------------------------------------------------------------
Const cPROGSCAN       =    5
Const cSensorOff      = -999     '* Code fuer nicht angeschlossene Sensoren

'*** Programm Konfiguration
Const cnSTATUS        =   10      '* Anzahl binaerer Status-Werte
Const cstVBATT        =    1      '*  - Binaerwert:      1
Const cstTARH         =    2      '*  - Binaerwert:      2
Const cstVWND         =    3      '*  - Binaerwert:      4
Const cstDWND         =    4      '*  - Binaerwert:      8
Const cstPLUV         =    5      '*  - Binaerwert:     16
Const cstSRAD         =    6      '*  - Binaerwert:     32
Const cstPAR          =    7      '*  - Binaerwert:     64
Const cstUVB          =    8      '*  - Binaerwert:    128
Const cstFTP          =    9      '*  - Binaerwert:    256
Const cstNTP          =   10      '*  - Binaerwert:    512
Const cnPARAMETER     =   30

ConstTable
  Const cMETEOINT     =   10      '* Speicherintervall der Meteodaten in Minuten
  Const cPPPOPENATMPT =    5      '* Anzahl Verbindungsversuche bei GPRS-Attach
  Const cFTP_IP       =   "ftp.wsl.ch"
  Const cFTP_User     =   "lwf_gprs"
  Const cFTP_Pwd      =   "password"
  Const cDataFileName =   "USR:LocalData.dat"
  Const cFTPFileName  =   "MET_N.dat"
EndConstTable 


'--------------------------------------------------------------------------------------
'-   V A R I A B L E N                                                                -
'--------------------------------------------------------------------------------------

'*** Stationskonfiguration
Public StationID As Long
'---------------------------------------
' Weitere Variabeln (Peter Waldner, 8.9.2014)
Public Proj1 As Long
Public Proj22 As Long
Public Proj30 As Long
'---------------------------------------
Public ProgVers
Public StatusIn As Long

'*** Messgroessen:
Public VBATact
Public TLOGact
Public TAIRact
Public RHUMact
Public VWNDact
Public VWNDmax
Public DWNDact
Public PLUVact
Public SRADact
Public PARact
Public UVBact

'*** Programmsteuerung:
Public FlagMeteoData     As Boolean
Public FlagProj22Data    As Boolean
Public FlagConfigData    As Boolean
Public FlagBatSave       As Boolean
Public FlagForceMeasure  As Boolean
Public FlagEnterStatus   As Boolean
Public FlagEnterConfig   As Boolean
Public FlagTimeSync      As Boolean
Public FlagPPPOpen       As Boolean
Public FlagPPPClose      As Boolean
Public FlagInit          As Long

'*** Programmkonfiguration:
Public Quality As Long
Public Status(cnSTATUS) As Long
 Alias Status(cstVBATT)  = StatBATT
 Alias Status(cstTARH)   = StatTARH
 Alias Status(cstVWND)   = StatVWND
 Alias Status(cstDWND)   = StatDWND
 Alias Status(cstPLUV)   = StatPLUV
 Alias Status(cstSRAD)   = StatSRAD
 Alias Status(cstPAR)    = StatPAR
 Alias Status(cstUVB)   = StatUVB
 Alias Status(cstFTP)    = StatFTP
 Alias Status(cstNTP)    = StatNTP
Public PluvCali
Public SRadCali
Public PARCali
Public UVBCali
Public VBATlim

'*** Umrechnungen von Messgroessen:
Public PluvSum
Public Pluv10m
Public VBATmean
Public VBAT5h
Dim    TAIR
Dim    RHUM
Dim    VWND
Dim    DWND
Dim    SRAD
Dim    PAR
Dim    UVB
Dim    VBAT
Dim    TLOG

'***   FTP-Filetransfer
Public FlagFTP As Long
Public DataLine As String * 500
Public DataFile As Long
Public pppIPAddress As String * 30
Public pppCloseSuccess As Boolean
Public pppOpenState As Boolean
Public pppOpenCount As Long
Public FTPSendSuccess As Boolean
Public NTPOffset As Long
Public FTPCount As Long

'***   Konfiguration GSM-Modem
Public FlagConfigGSM As Long
Public GSMCommandString As String * 100
Public GSMAnswer As String * 1000
Dim    SerialInput As String * 100

'*** Zeitvariable
Dim    ActualTime(9)
 Alias ActualTime(1) = Year
 Alias ActualTime(2) = Month
 Alias ActualTime(3) = DayOfMonth
 Alias ActualTime(4) = hour
 Alias ActualTime(5) = minute
 Alias ActualTime(6) = second
 Alias ActualTime(7) = uSecond
 Alias ActualTime(8) = DayOfWeek
 Alias ActualTime(9) = DayOfYear

'*** Masseinheiten
Units TAIRact = deg_C
Units RHUMact = %
Units VWNDact = m/s
Units VWNDmax = m/s
Units Pluv10m = mm/10min
Units PluvSum = mm
Units DWNDact = deg
Units SRADact = W/m2
Units PARact  = umol/m2s
Units UVBact  = W/m2
Units VBATact = V
Units TLOGact = deg_C

'*** Hilfsvariablen
Dim iCnt As Long
Dim ParameterArray(cnPARAMETER)

'-----------------------------------------------
' Weitere Variabeln (Peter Waldner, 8.9.2014)
'*** Hilfsvariabeln Loops Multiplexer (Cnt) und Loop Sensoren pro Set (i)
Dim nCnt As Long
Dim iStart As Long
Dim iEnd As Long
Dim i As Long

'*** Intervall Proj22
Const Proj22Int = 1

'*** Sensoren 10HS
Const nHS = 2 ' Anzahl Sensoren (1,2,3,...)
Public HSmeas(nHS)
Public HScali(nHS)
Public HSoffset(nHS)
Public HSact(nHS)
Units HSact = m3/m-3

'*** Sensoren MPS Matrixpotential(hPa) und Temperatur (Degree Celsius)
Const nMPS = 2 ' Anzahl Sensoren (2,4,6,...)
Public MPmeas(2*nMPS) As Long
Public MPS(nMPS) As Long
Public MPT(nMPS) As Long
Units MPS = hPa
Units MPT = C

'*** Sensoren EC
Const nEC = 6 ' Anzahl Sensoren (3,6,9,...)
Public ECmeas(nEC)
Public ECcali(nEC)
Public ECoffset(nEC)
Public ECact(nEC)
Units ECact = m3/m-3

'*** Stammabflusswippen
Const nSF = 5 ' Anzahl
Public SFact(nSF)
Public SF10m(nSF)
'--------------------------------------------------

'**************************************************************************************
'*                                                                                    *
'*   D A T E N - T A B L E S                                                          *
'*                                                                                    *
'**************************************************************************************
DataTable (Meteo,FlagMeteoData,-1)
	DataInterval (0,cMETEOINT,Min,10)
  Sample(1,StationID,IEEE4)
	FieldNames("Stationskennung")
	' --------------------------------------------
	' Weitere Spalte (Peter Waldner, 8.9.2014)
  Sample(1,Proj1,IEEE4)
	FieldNames("MProjNr")
  Sample(1,cMETEOINT,FP2)
	FieldNames("Messintervall")
	' --------------------------------------------
  Average(1,TAIRact,FP2,False)
	FieldNames("Lufttemperatur")
  Average(1,RHUMact,FP2,False)
	FieldNames("Luftfeuchtigkeit")
	WindVector(1,VWNDact,DWNDact,FP2,False,0,0,1)
  FieldNames("Windgeschwindigkeit, Windrichtung")
  Sample(1,VWNDmax,FP2)
	FieldNames("Windspitze")
  Sample(1,Pluv10m,FP2)
	FieldNames("Niederschlag")
  Sample(1,PluvSum,FP2)
	FieldNames("Niederschlagssumme")
  Average(1,SRADact,FP2,False)
	FieldNames("Globalstrahlung")
  Average(1,PARact,FP2,False)
	FieldNames("phot.akt.Strahlung")
  Average(1,UVBact,FP2,False)
	FieldNames("UVB_Strahlung")
	Average(1,VBATact,FP2,False)
	FieldNames("Batteriespannung")
	Average(1,TLOGact,FP2,False)
	FieldNames("Loggertemperatur")
	Sample(1,Quality,FP2)
	FieldNames("Qualitaet")
EndTable

DataTable (Konfiguration,FlagConfigData,50)
  Sample(1,StationID,IEEE4)
	FieldNames("Stationskennung")
  Sample(1,Status,IEEE4)
	FieldNames("Status")
  Sample(1,PluvCali,FP2)
	FieldNames("Faktor_PLUV")
  Sample(1,SRadCali,FP2)
	FieldNames("Faktor_SRAD")
  Sample(1,PARCali,FP2)
	FieldNames("Faktor_PAR")
  Sample(1,UVBCali,FP2)
	FieldNames("Faktor_UVB")
EndTable


'---------------------------------------------
' Weitere DateTable (Peter Waldner,  8.9.2014)
DataTable (Proj22,FlagProj22Data,-1)
	DataInterval (0,cMETEOINT,Min,10)
  Sample(1,StationID,IEEE4)
	FieldNames("Stationskennung")
  Sample(1,Proj22,IEEE4)
	FieldNames("MProjNr")
	' 10HS
  Average(nHS,HSact,FP2,False)
	FieldNames("HS")
	' MPS-2 Matrixpotential
  Average(nMPS,MPS,FP2,False)
	FieldNames("MPS")
	' MPS-2 Temperatur
  Average(nMPS,MPT,FP2,False)
	FieldNames("MPT")
	' EC-5
  Average(nEC,ECact,FP2,False)
	FieldNames("EC")
	' SF
  Sample(nSF,SF10m,FP2)
	FieldNames("SF")	
EndTable
'---------------------------------------------


'**************************************************************************************
'*                                                                                    *
'*   S U B R O U T I N E N                                                            *
'*                                                                                    *
'**************************************************************************************

'--------------------------------------------------------------------------------------
'-   SUBInitialize: Programm initialisieren   -----------------------------------------
Sub SUBInitialize
  FlagMeteoData    = True
  FlagConfigData   = True
  FlagBatSave      = False
  FlagForceMeasure = False
  FlagEnterStatus  = False
  FlagEnterConfig  = False
  FlagTimeSync     = False
  FlagPPPOpen      = False
  FlagPPPClose     = False
  pppOpenState     = False
  pppCloseSuccess  = PPPClose
  ' Programmparameter einlesen / initialisieren
  Call SUBReadParameter
  Call SUBInitStatus
  FlagInit         = 1
EndSub '---   End SUBInitialize   -----------------------------------------------------

'**************************************************************************************
'***   SUBDefaultParameter: Initialisierung mit Defaultwerten    *******************
Sub SUBDefaultParameter
  StationID        =       1   '* Stations-Identifikationsnummer
  StatusIn         =    1023   '* Status (binaere Sensorkonfiguration)
  PluvCali         =     0.2   '* Wippenvolumen Niederschlagssensor
  SRadCali         =     100   '* Kalibrationsfaktor SKS1110
  PARCali          =     100   '* Kalibrationsfaktor SKP 210/I
  UVBCali          = 6.66667   '* Kalibrationsfaktor SKU 430/I
  VBATlim          =    12.4   '* Grenzspannung fuer Batteriesparmode
  ProgVers         =     1.1   '* Programmversion
EndSub

'**************************************************************************************
'***   SUBReadParameter: Einlesen der Programmparameter   *****************************
Sub SUBReadParameter  
  ParameterArray(1) = -9999
  Calfile(ParameterArray(),cnPARAMETER,"USR:Parameter.cal",1)
  If ParameterArray(1) = -9999 Then
    ' Wenn kein Cal-File eingelesen wurde:
    Call SUBDefaultParameter
    Call SUBWriteParameter
    ExitSub
  EndIf
  ' Sensor-Kalibrationswerte
  StationID        = ParameterArray(1)
  StatusIn         = ParameterArray(2)
  PluvCali         = ParameterArray(3)
  SRadCali         = ParameterArray(4)
  PARCali          = ParameterArray(5)
  UVBCali          = ParameterArray(6)
  VBATlim          = ParameterArray(7)
  ProgVers         = ParameterArray(8)
'--------------------------------------
' Weitere Parameter, Peter Waldner, 8.9.2014
  ' direkt im Programm
  Proj1=1
  Proj22=22
  Call SubReadParam22  
'--------------------------------------
EndSub

'---------------------------------------------
' Weitere ReadParam (Peter Waldner,  8.9.2014)
Sub SUBReadParam22
  ' 10HS
  For i=1 To nHS
    HScali(i)=1
    HSoffset(i)=0
  Next i
  ' 10EC
  For i=1 To nEC
    ECcali(i)=0.00119
    ECoffset(i)=-0.401
  Next i
EndSub
'---------------------------------------------


'**************************************************************************************
'***   SUBWriteParameter: Programmparameter in CAL-/TXT-File schreiben   **************
Sub SUBWriteParameter
  ParameterArray(1) = StationID
  ParameterArray(2) = StatusIn
  ParameterArray(3) = PluvCali
  ParameterArray(4) = SRadCali
  ParameterArray(5) = PARCali
  ParameterArray(6) = UVBCali
  ParameterArray(7) = VBATlim
  ParameterArray(8) = ProgVers
  '***   Parameter schreiben
  Calfile (ParameterArray(),cnPARAMETER,"USR:Parameter.cal",0)
  FlagConfigData  = True
  FlagEnterConfig = False
EndSub

'--------------------------------------------------------------------------------------
'-   SUBCheckRange: range check to set status value   ---------------------------------
Sub SUBCheckRange(lVal, lMin, lMax, lStatInd)
  If Status(lStatInd) >= 1 Then
    If (lVal > lMax) OR (lVal < lMin) OR (lVal = NAN) Then
      Status(lStatInd) = Status(lStatInd) + 1
    EndIf
  EndIf
EndSub '---   End SUBCheckRange   -----------------------------------------------------

'--------------------------------------------------------------------------------------
'-   SUBEnterStatus: enter changed Status(n)-values and calculate StatusIn   ----------
Sub SUBEnterStatus
  Dim k1
  
  StatusIn = 0 
  For k1 = 1 To cnSTATUS
    If Status(k1) >= 1 Then
      StatusIn = StatusIn + INT(2^(k1-1)+0.3)
    EndIf
  Next k1
  FlagEnterStatus = False
EndSub '---   End SUBEnterStatus   ----------------------------------------------------

'--------------------------------------------------------------------------------------
'-   SUBInitStatus: Statuswerte aus binaerem Status initialisieren   ------------------
Sub SUBInitStatus
  Dim k2
  
  For k2 = 1 To cnSTATUS
    If ((StatusIn MOD (2^k2)) > (2^(k2-1)-0.5)) Then
      Status(k2) = 1
    Else
      Status(k2) = 0
    EndIf
  Next k2
EndSub '---   End SUBInitStatus   -----------------------------------------------------

'--------------------------------------------------------------------------------------
'-   SUBQuality: calculate binary quality from status values   ------------------------
Sub SUBQuality
  Dim k3
  
  Quality = 0
  For k3 = 1 To cnSTATUS
    If Status(k3) >= 2 Then
      Quality = Quality + INT(2^(k3-1)+0.3)
    EndIf
  Next k3
  Call SUBInitStatus
EndSub '---   End SUBQuality   --------------------------------------------------------

'--------------------------------------------------------------------------------------
'-   SUBGSM: Steuerung des GSM-Modems (Wavecom Fastrack XTEND FXT009)   ---------------
Sub SUBGSM
  If FlagBatSave Then
    '***   Stromsparmode: GSM eingeschaltet nur jeweils von   *************************
    '***   06:00-06:30 / 10:00-10:30 / 14:00-14:30 / 18:00-18:30
    If minute = 00 Then
      If hour = 6 OR hour = 10 OR hour = 14 OR hour = 18 Then
        SW12(0)
      EndIf
    ElseIf minute = 30
      If hour = 6 OR hour = 10 OR hour = 14 OR hour = 18 Then
        SW12(1)
        pppOpenState = False
      EndIf
    EndIf
  Else
    '---   ohne Stromsparmode Reset taeglich um 11:55 Uhr
    If IfTime(715,1440,Min) Then
      SW12(1)
      pppOpenState = False
    Else
      SW12(0)
    EndIf
  EndIf
  '---   einmal stuendlich GPRS-Verbindung schliessen
  If TimeIntoInterval(0,60,Min) Then
    FlagPPPClose = True
  EndIf
  '---   falls FTP ausgeschaltet stuendlich GPRS-Verbindung oeffnen
  If StatFTP = 0 Then
    If TimeIntoInterval(10,60,Min) Then
      FlagPPPOpen = True
      pppOpenCount = 0
    EndIf
  EndIf
EndSub '---   End SUBGSM   ------------------------------------------------------------

'--------------------------------------------------------------------------------------
'-   SUBConfigGSM: Seriell-Befehle an GSM-Modem (Wavecom  -  RS232, 9600baud)   -------
Sub SUBConfigGSM
  Dim i As Long
  
  SerialOpen(ComRS232,9600,0,100,100)
  Delay(1,200,mSec)
  SerialFlush(ComRS232)
  SerialOut(ComRS232,GSMCommandString & CHR(13),"",1,10)
  GSMAnswer = ""
  For i = 1 To 10
    SerialIn(SerialInput,ComRS232,100,13,100)
    GSMAnswer = GSMAnswer & SerialInput
  Next i
  SerialFlush(ComRS232)
  SerialClose(ComRS232)
EndSub '---   End SUBConfigGSM   ------------------------------------------------------



'**************************************************************************************
'*                                                                                    *
'*   H A U P T P R O G R A M M                                                        *
'*                                                                                    *
'**************************************************************************************
BeginProg
	Scan (cPROGSCAN,Sec,0,0)
	  RealTime(ActualTime)
    If FlagInit = 0 Then Call SUBInitialize
    If FlagEnterStatus Then Call SUBEnterStatus
    If FlagEnterConfig Then Call SUBWriteParameter
    
    '***   Einschalten Hygroclip HC2-S3 und SKU 430 (C6)   ****************************
    If FlagForceMeasure Then
      PortSet(6,1)
    ElseIf TimeIntoInterval(55,60,Sec) Then
      PortSet(6,1)
    EndIf
    If TimeIntoInterval(0,30,Min) Then
      FlagForceMeasure = False
    EndIf
    
    '***   Systemmessungen und GSM-Steuerung   ****************************************
 	  If TimeIntoInterval(0,60,Sec) Then
   	  Battery(VBATact)
   	  AvgRun(VBATmean,1,VBATact,30)
      If TimeIntoInterval(300,1440,Min) Then
        VBAT5h = VBATmean
        If VBAT5h > VBATlim Then
          FlagBatSave = False
        Else
          FlagBatSave = True
        EndIf
      EndIf
      PanelTemp(TLOGact,250)
      Call SUBGSM
    EndIf
    If FlagConfigGSM > 0 Then
      FlagConfigGSM = FlagConfigGSM - cPROGSCAN
      If FlagConfigGSM <= 0 Then
        FlagConfigGSM = 0
        Call SUBConfigGSM
      EndIf
    EndIf

    '***   Meteomessungen (5 Sekunden)   **********************************************
    '---   Windgeschwindigkeit (Campbell A100R: P1)   ---------------------------------
    If StatVWND >= 1 Then
      PulseCount(VWNDact,1,1,2,1,1.25,0)
      Call SUBCheckRange(VWNDact,0,70,cstVWND)
      If VWNDact > VWNDmax Then
        VWNDmax = VWNDact
      EndIf
    Else
      VWNDact = cSensorOff
      VWNDmax = cSensorOff
    EndIf
    '---   Windrichtung (Campbell W200P: SE4, Ex2)   ----------------------------------
    If StatDWND >= 1 Then
      BrHalf(DWNDact,1,mV2500,4,Vx2,1,2500,False,20000,250,357,0)
      Call SUBCheckRange(DWNDact,-1,361,cstDWND)
    Else
      DWNDact = cSensorOff
    EndIf
    '---   Niederschlag (Campbell ARG100: P2)   ---------------------------------------
    If StatPLUV >= 1 Then
      PulseCount(PLUVact,1,2,2,0,PluvCali,0)
      PluvSum = PluvSum + PLUVact
      Pluv10m = Pluv10m + PLUVact
    Else
      PLUVact = cSensorOff
      PluvSum = cSensorOff
      Pluv10m = cSensorOff
    EndIf

    '***   Meteomessungen (60 Sekunden)   *********************************************
    If TimeIntoInterval(0,60,Sec) OR FlagForceMeasure Then
      '---   Lufttemperatur und -feuchte (Rotronic Hygroclip HC2-S3:  SE3/2, C6) ------
      If StatTARH >= 1 Then
        VoltSe(TAIRact,1,mv2500,3,False,0,_50Hz,0.1,-40)
        VoltSe(RHUMact,1,mv2500,2,False,0,_50Hz,0.1,0)      
        Call SUBCheckRange(TAIRact,-40,50,cstTARH)
        Call SUBCheckRange(RHUMact,0,100,cstTARH)
      Else
        TAIRact = cSensorOff
        RHUMact = cSensorOff
      EndIf

      '---   kurzwellige Strahlung (SKS1110: SE7)   -----------------------------------
      If StatSRAD >= 1 Then
        VoltSe(SRADact,1,mV25,7,False,0,250,SRadCali,0)
        SUBCheckRange(SRADact,0,2000,cstSRAD)
      Else
        SRADact = cSensorOff
      EndIf

      '---   photosynthetisch aktive Strahlung (SKP 210/I: SE8)   ---------------------
      If StatPAR >= 1 Then
        VoltSe(PARact,1,mV250,8,False,0,250,PARCali,0)
        SUBCheckRange(PARact,0,10000,cstPAR)
      Else
        PARact = cSensorOff
      EndIf

      '---   Ultraviolett-B Strahlung (SKU 430/I: Diff3)   ----------------------------
      If StatUVB >= 1 Then
        VoltDiff(UVBact,1,mV2500,3,False,0,250,UVBCali,0)
        SUBCheckRange(UVBact,0,10000,cstUVB)
      Else
        UVBact = cSensorOff
      EndIf

      '---   Speisung Hygroclip und SKU 430 ausschalten   -----------------------------
      If NOT FlagForceMeasure Then
        PortSet(6,0)
      EndIf
    EndIf

    '***   Multiplexer Mux1 (10 Minuten)   ********************************************
'-------------------------------------
' Peter Waldner, 8.9.2014
    ' Testen direkt verdrahtet
    'If TimeIntoInterval(0,10,Sec) Then
      ' EC-5: rot: SE13, weiss: VX1, blank: AG
      'BrHalf(ECmeas(1),1,mV2500,13,Vx1,1,2500,False,10000,_50Hz,2500,0)
      ' 10HS: rot: SE14, weiss(!): 12V, blank: AG
      'VoltSe(HSmeas(1),1,mV2500,11,False,10000,_50Hz,1,0)
      'i=1
      'HSact(i)=0.00000000297 * HSmeas(i)^3 - 0.00000737 * HSmeas(i)^2 + 0.00669 * HSmeas(i) - 1.92
      ' MPS-2: rot: C5, weiss(!): 5V, blank: G
      'SDI12Recorder (MPSmeas(),1,0,"M!",1.0,0)
    'EndIf

    '--- MESSUNG SENSOREN: PROGRAMMAENDERUNGEN
    '    allgemein
    '    - istart, iend, i, nCnt
    '    Sensor spezifisch, z.B.
    '    - cn...
    '    - Array ...meas(), ...cali(), ...offsett(), ...act()
    '    - readParam, writeParam, defaultParam
    '    - DataTable
    '    - ftp
    '    - Projekt        

    ' MUX 1 (Res: C8, Clk: C4)
    ' --- MESSUNG 10HS Sensoren am Multiplexer 1, 2 pro Set (HS)
    '    Anschluss: rot: H1 (SE9) oder L1 (SE10)
    '               white: L2 (5V/12V)
    '               blank: G
    '    Anzahl:    nHS
    If TimeIntoInterval(0,Proj22Int,Min) Then
      '* Multiplexer 1 aktivieren
      PortSet(8,1)
      Delay(1,1,mSec)
      '* Multiplexer durchschalten:
      nCnt = nHS
      For iCnt = 1 To nCnt
        PulsePort(4,10000)
        Delay(1,10,Sec)
        ' Messung
        ' 10HS: rot: SE9, weiss(!): 12V, blank: AG
        VoltSe(HSmeas(iCnt),1,mV2500,9,False,10000,_50Hz,1,0)
        ' Kalibration
        i=iCnt
          ' HSact(i) = HScali(i) * HSmeas(i) + HSoffset(i)
          HSact(i)=0.00000000297 * HSmeas(i)^3 - 0.00000737 * HSmeas(i)^2 + 0.00669 * HSmeas(i) - 1.92
        ' End Kalibration
      Next iCnt
      '* Mulitplexer 1 deaktivieren
      PortSet(8,0)
      Delay(1,10,mSec)
    EndIf ' time 
    
    '--- MESSUNG MPS-2 SENSOREN AM MULTIPLEXER 1, 1 pro Set (MPS)
    '    Anschluss: rot: H2 (C5)
    '               white: L2 (5V/12V)
    '               blank: G
    If TimeIntoInterval(0,Proj22Int,Min) Then
      '* Multiplexer 1 aktivieren
      PortSet(8,1)
      Delay(1,1,mSec)
      '* Multiplexer durchschalten:
      nCnt = nMPS
      For iCnt = 1 To nCnt
        PulsePort(4,10000)
        Delay(1,1,mSec)
        ' Messung
        SDI12Recorder(MPmeas(),5,0,"M!",1,0)
        ' Kalibration und keine Messung 
        i=iCnt
        If MPS(i) = NAN Then
          MPS(i)=cSensorOff
          MPT(i)=cSensorOff
        Else
          MPS(i)=MPmeas(iCnt*2-1)
          MPT(i)=MPmeas(iCnt*2-0)
        EndIf
        ' End Kalibration
      Next iCnt
      '* Mulitplexer 1 deaktivieren
      PortSet(8,0)
      Delay(1,10,mSec)
    EndIf ' time 
    ' END MUX 1

'====================================================================
'--------------------------------------------------------------------
     ' Projekt 22: Bodenwassergehalt EC-5               alle 60 Minuten
     ' EC-5 Sensoren an Mulltiplexer 2, 3 pro Set, Set 1-nEC/3 (EC)
     ' Multiplexer 2: res-C7, clk-C4. 
     ' Sensoren:   rot1-H1-SE13, rot2-L1-SE14, rot3-H2-SE15
     '             weiss123 - L2 - VX1
     '             blank123 - AG - AG
     If Proj22 > 0 Then

      ' -----------------------------------------------------
      ' *** Projekt 22: Messen 
      If TimeIntoInterval(0,Proj22Int,Min) Then
        '* Multiplexer 1 aktivieren
        Delay(1,10,mSec)
        PortSet(7,1)
        Delay(1,10,mSec)
        '* Multiplexer durchschalten:
        '* Messen ab Set 1
        nCnt=nEC/3
        ' nCnt= 0
        For iCnt = 1 To nCnt
          PulsePort(4,10000)
          Delay(1,10,mSec)
          ' Messung SE11 bis SE14
          ' EC-5: rot: SE13, weiss: VX1, blank: AG
          BrHalf(ECmeas(iCnt),3,mV2500,13,Vx1,3,2500,False,10000,_50Hz,2500,0)
          ' Kalibration
          iStart=3*iCnt-2
          iEnd=3*iCnt
          For i=iStart To iEnd
            ECact(i)=ECcali(i)*ECmeas(i)+ECoffset(i)
          Next i
          ' End Kalibration
        Next iCnt
        '* Mulitplexer 1 deaktivieren
        PortSet(7,0)
        Delay(1,10,mSec)
      EndIf ' time 

      '-----------------------------------------------------
      '*** Projekt 22: Datenablage 
      If TimeIntoInterval(0,Proj22Int,Min) Then
        ' Datenablage
        CallTable Proj22
      EndIf

      '-----------------------------------------------------
      '*** Projekt 22: FTP vorbereiten
    

      '-----------------------------------------------------
      '*** Projekt 22: Reset Aggregieren

    EndIf ' Projekt 22
'========================================================  


'====================================================================
'--------------------------------------------------------------------
    ' Projekt: Stammabflusswippen
 
    '--- MESSUNG Stammabflusswippen an SDM-SW8A
    '    Anschluss: rot: 12V
    '               weiss: P1 bis P5
    '               schwarz: G
      If TimeIntoInterval(0,10,Sec) Then
        SDMSW8A (SFact(),5,0,2,1,1.0,0)
        For i=1 To nSF
          SF10m(i)=SF10m(i)+SFact(i)
        Next i
      EndIf
    
      '-----------------------------------------------------
      '*** Projekt 22: Datenablage 
      If TimeIntoInterval(0,Proj22Int,Min) Then
        ' Datenablage
        CallTable Proj22

        ' FTP vorbereiten
        'LWF Format fuer CR1000 (TOA5,StatNr,MProjNr,Intervall,Folgenr1,...)
        DataLine = FormatFloat(Year,"%04.0f") & "-"
        DataLine = DataLine & FormatFloat(Month,"%02.0f") & "-"
        DataLine = DataLine & FormatFloat(DayOfMonth,"%02.0f") & " "
        DataLine = DataLine & FormatFloat(hour,"%02.0f") & ":"
        DataLine = DataLine & FormatFloat(minute,"%02.0f") & ","
        DataLine = DataLine & FormatFloat(StationID,"%.0f") & "," 
        DataLine = DataLine & FormatFloat(Proj22,"%.0f") & "," 
        DataLine = DataLine & FormatFloat(Proj22Int,"%.0f") & "," 
        For i=1 To nEC
          DataLine = DataLine & ECact(i) & "," 
        Next i
        DataLine = DataLine & CHR(10)
        DataFile = FileOpen(cDataFileName,"a",-1)
        FileWrite(DataFile,DataLine,0)
        FileClose(DataFile)
      
        ' Reset Aggregierung
        For i=1 To nSF
          SF10m(i)=0
        Next i
      EndIf
   EndIf
'--------------------------------------------------

    '***   Daten-Ablage Projekt 1 *************************************************************
    If TimeIntoInterval(0,cMETEOINT,Min) Then
      Call SUBQuality
    EndIf
		CallTable Meteo
    CallTable Konfiguration
    
    FlagConfigData = False

    '***   FTP File Projekt 1 ********************************************
    If StatFTP >= 1 Then
      If TimeIntoInterval(0,cMETEOINT,Min) Then
        TAIR    = Meteo.Lufttemperatur(1,1)
        RHUM    = Meteo.Luftfeuchtigkeit(1,1)
        VWND    = Meteo.Windgeschwindigkeit(1,1)
        DWND    = Meteo.Windrichtung(1,1)
        SRAD    = Meteo.Globalstrahlung(1,1)
        PAR     = Meteo.phot.akt.Strahlung(1,1)
        UVB     = Meteo.UVB_Strahlung(1,1)
        VBAT    = Meteo.Batteriespannung(1,1)
        TLOG    = Meteo.Loggertemperatur(1,1)
        If StatTARH = 0 Then
          TAIR = cSensorOff
          RHUM = cSensorOff
        EndIf
        If StatVWND = 0 Then
          VWND = cSensorOff
          VWNDmax = cSensorOff
        EndIf
        If StatDWND = 0 Then
          DWND = cSensorOff
        EndIf
        If StatSRAD = 0 Then
          SRAD = cSensorOff
        EndIf
        If StatPAR = 0 Then
          PAR = cSensorOff
        EndIf
        If StatUVB = 0 Then
          UVB = cSensorOff
        EndIf        
        '- Datenzeile ins lokale Datenfile schreiben
 '-----------------------------------
 '                          Peter Waldner, 9.9.2014
        'LWF Format fuer CR1000 (TOA5,StatNr,MProjNr,Intervall,Folgenr1,...)
        DataLine = FormatFloat(Year,"%04.0f") & "-"
        DataLine = DataLine & FormatFloat(Month,"%02.0f") & "-"
        DataLine = DataLine & FormatFloat(DayOfMonth,"%02.0f") & " "
        DataLine = DataLine & FormatFloat(hour,"%02.0f") & ":"
        DataLine = DataLine & FormatFloat(minute,"%02.0f") & ","
        DataLine = DataLine & FormatFloat(StationID,"%.0f") & "," 
        DataLine = DataLine & FormatFloat(Proj1,"%.0f") & "," 
        DataLine = DataLine & FormatFloat(cMETEOINT,"%.0f") & "," 
 '-----------------------------------
        DataLine = DataLine & TAIR & "," & RHUM & "," & VWND & ","
        DataLine = DataLine & DWND & "," & VWNDmax & "," & Pluv10m & ","
        DataLine = DataLine & PluvSum & "," & SRAD & "," & PAR & "," & UVB
        DataLine = DataLine & VBAT & "," & TLOG & "," & Quality & CHR(10)
        DataFile = FileOpen(cDataFileName,"a",-1)
        FileWrite(DataFile,DataLine,0)
        FileClose(DataFile)
        '---   FTP-Transfer initialisieren
        If NOT pppOpenState Then pppOpenCount = 0
        FTPSendSuccess = False
        FlagFTP = 1
      EndIf
    EndIf
    
    If TimeIntoInterval(0,1440,Min) Then
      FTPCount = 0
      If StatNTP >=1 Then
        FlagTimeSync = True
      EndIf
    EndIf

    '***   Reset der Meteo-Werte   ****************************************************
    If TimeIntoInterval(0,cMETEOINT,Min) Then
      Pluv10m = 0
      VWNDmax = 0
    EndIf
    If TimeIntoInterval(0,1440,Min) Then
      '---   Reset der laufenden Jahres-Niederschlagssumme jeweils am 1. Mai
      If Month = 5 AND DayOfMonth = 1 Then
        PluvSum = 0
      EndIf
    EndIf

	NextScan



'**************************************************************************************
'***																																								***
'***   F T P - D A T E N T R A N S F E R    			  																***
'***																																						    ***
'**************************************************************************************
  SlowSequence
  Scan(30,Sec,5,0)
    '---   Zeitsynchronisation mit "pool.ntp.org"   -----------------------------------
    If FlagTimeSync AND pppOpenState Then
      NTPOffset = NetworkTimeProtocol ("pool.ntp.org",3600,1000)
      FlagTimeSync = False
      Delay(1,10,Sec)
    EndIf
    '---   PPP-Verbindung aufbauen, falls nicht bereits offen   -----------------------
    If FlagFTP = 1 AND pppOpenState Then
      FlagFTP = 11
    EndIf
    If (FlagFTP = 1 AND NOT pppOpenState) OR FlagPPPOpen Then
      '---   PPP-Verbindung aufbauen
      SerialOpen(ComRS232,9600,0,100,100)
      Delay(1,200,mSec)
      SerialFlush(ComRS232)
      SerialOut(ComRS232,"AT+CGDCONT=1,""IP"",""gprs.swisscom.ch""" & CHR(13),"",1,10)
      SerialClose(ComRS232)
      pppOpenCount = pppOpenCount + 1
      pppIPAddress = PPPOpen
      If pppIPAddress <> "0.0.0.0" Then
        pppOpenState = True
        FlagPPPOpen  = False
        If FlagFTP = 1 Then FlagFTP = 11
      ElseIf pppOpenCount >= cPPPOPENATMPT Then
        '---   PPP-Verbindung fehlgeschlagen, Abbruch nach cPPPOPENATMPT Versuchen
        pppCloseSuccess = PPPClose
        FlagPPPOpen  = False
        FlagFTP = 0
      EndIf
    '---   Datenfile per FTP schicken   -----------------------------------------------
    ElseIf FlagFTP = 11 Then
      ' FTP Datenuebertragung (nur ein Versuch, bei Misserfolg File stehenlassen)
      If FTPClient(cFTP_IP,cFTP_User,cFTP_Pwd,cDataFileName,cFTPFileName,0) Then
        FileManage(cDataFileName,8)
        FTPSendSuccess = True
        FTPCount = FTPCount + 1
      EndIf
      FlagFTP = 12
      ' falls FlagPPPClose, dann GPRS-Verbindung nach Transfer schliessen
    ElseIf FlagFTP = 12 OR FlagFTP = 0 Then
      If FlagPPPClose Then
        pppCloseSuccess = PPPClose
        If pppCloseSuccess Then
          pppOpenState = False
          FlagPPPClose = False
          FlagFTP = 0
        EndIf
      EndIf
    EndIf
  Next Scan
EndProg

